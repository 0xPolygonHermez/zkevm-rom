; This file is WIP

VAR GLOBAL tmpVarA
VAR GLOBAL tmpVarB
VAR GLOBAL tmpVarC
VAR GLOBAL tmpVarD
VAR GLOBAL tmpVarE
VAR GLOBAL tmpZkPC

; @info Get absolute value and sign
; @in A => number to convert
; @out A => Absolut value of A
; @out B => Sign of A
abs:
    57896044618658097711785492504343953926634992332820282019728792003956564819967n => B
    $ => B          :GT
    B - 1           :JMPC(endca2) ; no conversion if positive number
                    :JMP(ca2)
; @info complement a 2 conversion
; @in A => number to convert
; @out A => number converted in ca2
ca2:
    B               :MSTORE(tmpVarB)
    115792089237316195423570985008687907853269984665640564039457584007913129639935n => B
    $ => A          :XOR
    ${A + 1} => A
    $ => B          :MLOAD(tmpVarB)
endca2:
                    :RETURN

; @info copy MEM A to ctxB SP = 1024
; TODO: copy + 32 bytes
copySP:
    RR              :MSTORE(tmpZkPC)
    CTX             :MSTORE(currentCTX)
    1024 => SP                             ;destOffset = 0
    $ => CTX        :MLOAD(originCTX)
    $ => E          :MLOAD(argsOffsetCall) ;offset
    $ => C          :MLOAD(argsLengthCall) ;length
    0 - ${E%32}     :JMPC(copyInit2)

copyInit:
    C - 1           :JMPC(copyEnd)
    C - 32          :JMPC(copyFinal)
    $ => A          :MLOAD(MEM:E)
    E + 32 => E
    $ => CTX        :MLOAD(currentCTX)
    A               :MSTORE(SP++)
    $ => CTX        :MLOAD(originCTX)
    C - 32 => C
                    :JMP(copyInit)

copyFinal:
    $ => A          :MLOAD(MEM:E)
    32 - C => D
    zkPC+1 => RR    :JMP(SHRarith)
    zkPC+1 => RR    :JMP(SHLarith)
    $ => CTX        :MLOAD(currentCTX)
    A               :MSTORE(SP++)
                    :JMP(copyEnd)

copyInit2:
    C - 1           :JMPC(copyEnd)
    C - 32          :JMPC(copyFinal2)
    ${E%32} => D
    ${(E/32)*32} => E
    $ => A          :MLOAD(MEM:E)
    ${A << (D*8)} => B
    32 - D => D
    E + 32 => E
    $ => A          :MLOAD(MEM:E)
    zkPC+1 => RR    :JMP(SHRarith)
    $ => CTX        :MLOAD(currentCTX)
    A + B           :MSTORE(SP++)
    $ => CTX        :MLOAD(originCTX)
    C - 32 => C
                    :JMP(copyInit2)

copyFinal2:
    32 - ${E%32} - C => D       :JMPC(copyFinal22)
    ${E%32} => B
    ${(E/32)*32} => E
    $ => A                      :MLOAD(MEM:E)
    32 - C => D
    zkPC+1 => RR                :JMP(SHRarith)
    ${A << (D*8)} => A
    $ => CTX                    :MLOAD(currentCTX)
    A                           :MSTORE(SP++)
                                :JMP(copyEnd)

copyFinal22:
    ${E%32} => D
    ${(E/32)*32} => E
    $ => A                  :MLOAD(MEM:E)
    ${A << (D*8)} => B
    E + 32 => E
    $ => A                  :MLOAD(MEM:E)
    C - 32 + D => D
    32 - D => D
    zkPC+1 => RR            :JMP(SHRarith)
    32 - C => D
    ${A << (D*8)} => A
    $ => CTX                :MLOAD(currentCTX)
    A + B                   :MSTORE(SP++)

copyEnd:
    $ => CTX        :MLOAD(currentCTX)
    $ => RR         :MLOAD(tmpZkPC)
                    :RETURN

; @info byte length of B
; @in B => number
; @out A => bytes length
getLenBytes:
    RR              :MSTORE(tmpZkPC)
    B               :MSTORE(tmpVarB)
    C               :MSTORE(tmpVarC)
    0 => C
    B => A

getLenBytesLoop:
    0 => B
    $ => B          :EQ
    0 - B           :JMPC(getLenEnd)
    1 => D
    zkPC+1 => RR    :JMP(SHRarith)
    C + 1 => C
                    :JMP(getLenBytesLoop)

getLenEnd:
    C => A
    $ => B          :MLOAD(tmpVarB)
    $ => C          :MLOAD(tmpVarC)
    $ => RR         :MLOAD(tmpZkPC)
                    :RETURN

; @info save value to memory 32 bytes with offset
; REQUIRE: set bytesToStore with value to use in MSTORE
; @in E => offset
; @out E => new offset

VAR GLOBAL bytesToStore

MSTORE32:
    RR              :MSTORE(tmpZkPC)
    A               :MSTORE(tmpVarA)
    B               :MSTORE(tmpVarB)
    C               :MSTORE(tmpVarC)
    D               :MSTORE(tmpVarD)
    E               :MSTORE(tmpVarE)
    ${E%32} - 1     :JMPC(MSTORE322)
    ${E%32} => C
    ${(E/32)*32} => E
    $ => A          :MLOAD(MEM:E)
    32 - C => D
    zkPC+1 => RR    :JMP(SHRarith)
    ${A << (D*8)} => A
    A => B
    $ => A          :MLOAD(bytesToStore)
    C => D
    zkPC+1 => RR    :JMP(SHRarith)
    A + B           :MSTORE(MEM:E)
    E + 32 => E                                    ;new offset
    $ => A          :MLOAD(MEM:E)
    C => D
    ${A << (D*8)} => A
    zkPC+1 => RR    :JMP(SHRarith)
    A => B
    $ => A          :MLOAD(bytesToStore)
    32 - C => D
    ${A << (D*8)} => A
    A + B           :MSTORE(MEM:E)
    $ => E          :MLOAD(tmpVarE)
    E + 32 => E
                    :JMP(MSTOREend)

MSTORE322:
    $ => A          :MLOAD(bytesToStore)
    A               :MSTORE(MEM:E)
    E + 32 => E
                    :JMP(MSTOREend)

; @info save value to memory < 32 bytes with offset
; REQUIRE: set bytesToStore with value to use in MSTORE
; @in E => offset
; @in C => length
; @out E => new offset

MSTOREX:
    RR                          :MSTORE(tmpZkPC)
    A                           :MSTORE(tmpVarA)
    B                           :MSTORE(tmpVarB)
    C                           :MSTORE(tmpVarC)
    D                           :MSTORE(tmpVarD)
    E                           :MSTORE(tmpVarE)
    32 - ${E%32} - C            :JMPC(MSTOREX2)
    ${E%32} => D
    ${(E/32)*32} => E
    $ => A                      :MLOAD(bytesToStore)
    zkPC+1 => RR                :JMP(SHRarith)
    $ => A                      :MLOAD(MEM:E)
    32 - D => D
    zkPC+1 => RR                :JMP(SHRarith)
    ${A << (D*8)} => A
    A + B                       :MSTORE(MEM:E)
    $ => E                      :MLOAD(tmpVarE)
    E + C => E                                    ;new offset
                                :JMP(MSTOREend)

MSTOREX2:
    ${E%32} => D
    ${(E/32)*32} => E
    $ => A                      :MLOAD(bytesToStore)
    zkPC+1 => RR                :JMP(SHRarith)
    A => B
    $ => A                      :MLOAD(MEM:E)
    32 - D => D
    zkPC+1 => RR                :JMP(SHRarith)
    ${A << (D*8)} => A
    A + B                       :MSTORE(MEM:E)
    E + 32 => E
    $ => A                      :MLOAD(bytesToStore)
    ${A << (D*8)} => B
    $ => A                      :MLOAD(MEM:E)
    C - D => D
    zkPC+1 => RR                :JMP(SHRarith)
    A + B                       :MSTORE(MEM:E)
    $ => E                      :MLOAD(tmpVarE)
    E + C => E                                    ;new offset

MSTOREend:
    $ => A                      :MLOAD(tmpVarA)
    $ => B                      :MLOAD(tmpVarB)
    $ => C                      :MLOAD(tmpVarC)
    $ => D                      :MLOAD(tmpVarD)
    $ => RR                     :MLOAD(tmpZkPC)
                                :RETURN


; @info get value from memory (32 bytes)
; @in E => offset
; @out A => value
; @out E => new offset

MLOAD32:
    RR              :MSTORE(tmpZkPC)
    B               :MSTORE(tmpVarB)
    C               :MSTORE(tmpVarC)
    D               :MSTORE(tmpVarD)
    ${E%32} - 1     :JMPC(MLOAD322)
    ${E%32} => C
    ${(E/32)*32} => E
    $ => A          :MLOAD(MEM:E)
    C => D
    ${A << (D*8)} => B
    32 - C => D
    E + 32 => E
    $ => A          :MLOAD(MEM:E)
    zkPC+1 => RR    :JMP(SHRarith)
    A + B => A
    E + C => E                                      ;new offset
                    :JMP(MLOADend)

MLOAD322:
    $ => A          :MLOAD(MEM:E)
    E + 32 => E                                     ;new offset
                    :JMP(MLOADend)

; @info get value from memory (32 bytes)
; @in E => offset
; @in C => length
; @out A => value
; @out E => new offset

MLOADX:
    RR              :MSTORE(tmpZkPC)
    B               :MSTORE(tmpVarB)
    C               :MSTORE(tmpVarC)
    D               :MSTORE(tmpVarD)
    E               :MSTORE(tmpVarE)
    32 - ${E%32} - C => D       :JMPC(MLOADX2)
    ${E%32} => B
    ${(E/32)*32} => E
    $ => A                      :MLOAD(MEM:E)
    B => D
    ${A << (D*8)} => A
    32 - C => D
    zkPC+1 => RR                :JMP(SHRarith)
    ${A << (D*8)} => A
    E + B + C => E                                    ;new offset
                                :JMP(MLOADend)

MLOADX2:
    ${E%32} => D
    ${(E/32)*32} => E
    $ => A                  :MLOAD(MEM:E)
    ${A << (D*8)} => B
    E + 32 => E
    $ => A                  :MLOAD(MEM:E)
    C - 32 + D => D
    32 - D => D
    zkPC+1 => RR            :JMP(SHRarith)
    32 - C => D
    ${A << (D*8)} => A
    A + B => A
    E                       :MLOAD(tmpVarE)
    E + C => E                                    ;new offset

MLOADend:
    $ => B                      :MLOAD(tmpVarB)
    $ => C                      :MLOAD(tmpVarC)
    $ => D                      :MLOAD(tmpVarD)
    $ => RR                     :MLOAD(tmpZkPC)
                                :RETURN

; @info check account is empty ( balance == nonce == code == 0x )
; @in E => address
; @out E => isEmpty

ISEMPTY:
    A                           :MSTORE(tmpVarA)
    B                           :MSTORE(tmpVarB)
    C                           :MSTORE(tmpVarC)
    D                           :MSTORE(tmpVarD)
    ; read balance
    E => A
    0 => B,C
    $ => A                      :SLOAD
    $ => A                      :EQ
    A - 1                       :JMPC(ISEMPTYSet0)

    ; read nonce
    E => A
    1 => B
    $ => A                      :SLOAD
    0 => B
    $ => A                      :EQ
    A - 1                       :JMPC(ISEMPTYSet0)

    ; read bytecode
    E => A
    2 => B
    $ => A                      :SLOAD
    0 => B
    $ => A                      :EQ
    A - 1                       :JMPC(ISEMPTYSet0)

ISEMPTYSet1:
    1 => E
                                :JMP(ISEMPTYEnd)

ISEMPTYSet0:
    0 => E
                                :JMP(ISEMPTYEnd)

ISEMPTYEnd:
    $ => A                      :MLOAD(tmpVarA)
    $ => B                      :MLOAD(tmpVarB)
    $ => C                      :MLOAD(tmpVarC)
    $ => D                      :MLOAD(tmpVarD)
                                :RETURN

; @out A => min(requested_gas, all_but_one_64th)
gasSpendCallError:
    ; save tmp vars
    B                       :MSTORE(tmpVarB)
    C                       :MSTORE(tmpVarC)
    D                       :MSTORE(tmpVarD)
    E                       :MSTORE(tmpVarE)

    ; compute all_but_one_64th gas
    GAS - ${GAS/64} => A
    $ => B                  :MLOAD(gasCall)
    ; gas_sent_with_call = min(requested_gas, all_but_one_64th)
    $ => D                  :LT
    0 - D                   :JMPC(gasSpendCallErrorEnd)
    ; gas to substract
    B => A

gasSpendCallErrorEnd:
    $ => B                  :MLOAD(tmpVarB)
    $ => C                  :MLOAD(tmpVarC)
    $ => D                  :MLOAD(tmpVarD)
    $ => E                  :MLOAD(tmpVarE)
                            :RETURN

;@in E --> new mem length
saveMem:
    A                           :MSTORE(tmpVarA)
    B                           :MSTORE(tmpVarB)
    E                           :MSTORE(memLength)
    ${(E+31)/32} => A
    ${(B+31)/32} => B
    GAS - 3*A - ${A*A/512} => GAS
    GAS + 3*B + ${B*B/512} => GAS
    $ => A                      :MLOAD(tmpVarA)
    $ => B                      :MLOAD(tmpVarB)
                                :RETURN

;@in A - (A >> D)
;@in D - (A >> D)
;@out A -  A >> D => A
SHRarith:
    B                       :MSTORE(tmpVarB)
    C                       :MSTORE(tmpVarC)
    D                       :MSTORE(tmpVarD)
    E                       :MSTORE(tmpVarE)
    A => E
    ${exp(2,D*8)} => B
    ${B-1} => B
    $ => C                      :AND
    ${exp(2,D*8)} => B
    0 => A
    $ => A                      :EQ
    0 - A                       :JMPC(SHRarithfinal)
    ${E/B} => A
                                :JMP(SHRarithfinal)

SHRarithfinal:
    0 => D
    E                           :ARITH
    $ => B                      :MLOAD(tmpVarB)
    $ => C                      :MLOAD(tmpVarC)
    $ => D                      :MLOAD(tmpVarD)
    $ => E                      :MLOAD(tmpVarE)
                                :JMP(RR)
;
;SHRarith:
;    B                       :MSTORE(tmpVarB)
;    C                       :MSTORE(tmpVarC)
;    D                       :MSTORE(tmpVarD)
;    E                       :MSTORE(tmpVarE)
;    ${log(D)}
;    A => E
;    ${exp(2,D*8)} => B
;    ${B-1} => B
;    $ => C                      :AND
;    ${log(D)}
;    ${exp(2,D*8)} => B
;    0 => A
;    $ => A                      :EQ
;    A - 1                       :JMPC(SHRaritdiv0)
;    ${E/B} => A
;                                :JMP(SHRarithfinal)
;SHRaritdiv0:
;    0 => A
;
;SHRarithfinal:
;    0 => D
;    E                           :ARITH
;    $ => B                      :MLOAD(tmpVarB)
;    $ => C                      :MLOAD(tmpVarC)
;    $ => D                      :MLOAD(tmpVarD)
;    $ => E                      :MLOAD(tmpVarE)
;                                :JMP(RR)
;
;@in A - (A << D)
;@in D - (A << D)
;@out A -  A << D => A
SHLarith:
    B                       :MSTORE(tmpVarB)
    C                       :MSTORE(tmpVarC)
    D                       :MSTORE(tmpVarD)
    E                       :MSTORE(tmpVarE)
    A => E
    ${exp(2,D)} => B
    0 => C
    ${A*B} => E
    0 => D
    E                       :ARITH
    E => A
    $ => B                  :MLOAD(tmpVarB)
    $ => C                  :MLOAD(tmpVarC)
    $ => D                  :MLOAD(tmpVarD)
    $ => E                  :MLOAD(tmpVarE)
                            :JMP(RR)