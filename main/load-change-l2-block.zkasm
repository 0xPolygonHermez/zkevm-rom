INCLUDE "load-change-l2-block-utils.zkasm"
;;;;;;;;;;;;;;;;;;
;; ChangeL2BlockTx:
;;   - fields: [type | deltaTimestamp | indexL1InfoTree ]
;;   - bytes:  [  1  |       4        |        4        ]
;;;;;;;;;;;;;;;;;;

decodeChangeL2BlockTx:
    ; No changeL2BlockTx allowed at forced batches
    $                               :MLOAD(isForced), JMPNZ(invalidDecodeChangeL2Block)
    $ => E                          :MLOAD(batchHashDataPointer)
    $ => HASHPOS                    :MLOAD(batchHashPos)
    ; Decode deltaTimestamp / 4 bytes
    %DELTA_TIMESTAMP_NUM_BYTES => D :CALL(getChangeL2TxBytes)
    C + D => C
    A                               :MSTORE(deltaTimestamp)

    ; Decode indexL1InfoTree / 4 bytes
    %INDEX_L1INFOTREE_NUM_BYTES => D :CALL(getChangeL2TxBytes)
    C + D => C
    A                               :MSTORE(indexL1InfoTree)
    1                               :MSTORE(isChangeL2BlockTx)
    ; update batchHashPos
    HASHPOS                         :MSTORE(batchHashPos)
    ; update bytes parsed
    $ => A                      :MLOAD(batchL2DataParsed)
    A + C                       :MSTORE(batchL2DataParsed)
    ; increase number of transaction to process
    $ => A                      :MLOAD(pendingTxs)
    A + 1                       :MSTORE(pendingTxs), JMP(txLoopRLP)