INCLUDE "load-tx-rlp-utils.zkasm"
; Blocks RLP parsing
;       A - Initialization
;       B - Read and check RLP fields. Fill 'batchHashData' and Ethereum signed transaction bytes
;       C - Read signature. Fill 'batchHashData' bytes
;       D - Finish RLP parsing
;       E - Handler error RLP fields

;;;;;;;;;;;;;;;;;;
;; A - Initialization
;;     - Data to parse: [rlp(nonce, gasprice, gaslimit, to, value, data, chainId, 0, 0)|r|s|v]
;;     - Signed Ethereum transaction: H_keccak(rlp(nonce, gasprice, gaslimit, to, value, data, chainId, 0, 0))
;;     - RLP encoding information: https://ethereum.org/en/developers/docs/data-structures-and-encoding/rlp
;;     - Entire batch is discarded (no transaction is processed) if any error is found
;;;;;;;;;;;;;;;;;;

loadTx_rlp:
        ; A new hash with position 0 is started
        0 => HASHPOS

        ; We get a new hashId
        $ => E                          :MLOAD(lastHashKIdUsed)
        E+1 => E                        :MSTORE(lastHashKIdUsed)
        ; Pointer to next RLP bytes to read
        0 => C

;;;;;;;;;;;;;;;;;;
;; B - Read and check RLP fields. Fill 'batchHashData' and Ethereum signed transaction bytes
;;;;;;;;;;;;;;;;;;

;; Read RLP list length
        ; Should be a list
        1 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        A - 0xc0                        :JMPN(invalidTxRLP)
        A - 0xf8                        :JMPN(shortList)

longList:
        A - 0xf7 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
                                        :JMP(endList)
shortList:
        A - 0xc0 => A

endList:
        A + C                           :MSTORE(txRLPLength)

;; Read RLP 'nonce'
        ; 64 bits max
nonceREAD:
        1 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        A - 0x80                        :JMPN(endNonce)
        A - 0x81                        :JMPN(nonce0)
        A - 0x89                        :JMPN(shortNonce)
                                        :JMP(invalidTxRLP)

nonce0:
        0                               :MSTORE(lengthNonce)
        0 => A
                                        :JMP(endNonce)

shortNonce:
        A - 0x80 => D
        D                               :MSTORE(lengthNonce)
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)

endNonce:
        A                               :MSTORE(txNonce)


;; Read RLP 'gas price'
        ; 256 bits max
gasPriceREAD:
        1 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        A - 0x80                        :JMPN(endGasPrice)
        A - 0x81                        :JMPN(gasPrice0)
        A - 0xa1                        :JMPN(shortGasPrice)
                                        :JMP(invalidTxRLP)

gasPrice0:
        0 => A
                                        :JMP(endGasPrice)

shortGasPrice:
        A - 0x80 => D
        D - 1                           :JMPN(endGasPrice)
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)

endGasPrice:
        A                               :MSTORE(txGasPriceRLP)


;; Read RLP 'gas limit'
        ; 256 bits max
gasLimitREAD:
        1 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        A - 0x80                        :JMPN(endGasLimit)
        A - 0x81                        :JMPN(gasLimit0)
        A - 0xa1                        :JMPN(shortGasLimit)
                                        :JMP(invalidTxRLP)

gasLimit0:
        0 => A                          :JMPN(endGasLimit)

shortGasLimit:
        A - 0x80 => D
        D - 1                           :JMPN(endGasLimit)
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)

endGasLimit:
        A                               :MSTORE(txGasLimit)

;; Read RLP 'to'
        ; 160 bits max
toREAD:
        1 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        A - 0x80                        :JMPN(invalidTxRLP)
        A - 0x81                        :JMPN(noTo)
        A - 0x94                        :JMPN(invalidTxRLP)
        A - 0x95                        :JMPN(shortTo)
                                        :JMP(invalidTxRLP)

noTo:
        1                               :MSTORE(isCreateContract)
                                        :JMP(endTo)

shortTo:
        A - 0x80 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        A                               :MSTORE(txDestAddr)
        A                               :MSTORE(storageAddr)

endTo:


;; Read RLP 'value'
        ; 256 bits max
valueREAD:
        1 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        A - 0x80                        :JMPN(endValue)
        A - 0x81                        :JMPN(value0)
        A - 0xa1                        :JMPN(shortValue)
                                        :JMP(invalidTxRLP)

value0:
        0 => A
                                        :JMPN(endValue)

shortValue:
        A - 0x80 => D
        D - 1                           :JMPN(endValue)
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)

endValue:
        A                               :MSTORE(txValue)

;; Read RLP 'data'
        ; should not be a list
dataREAD:
        1 => D
        1024 => SP
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        $ => D                          :MLOAD(batchHashPos)
        D                               :MSTORE(dataStarts)
        A - 0x80                        :JMPN(veryShortData)
        A - 0x81                        :JMPN(endData)
        A - 0xb8                        :JMPN(shortData)
        A - 0xc0                        :JMPN(longData)
                                        :JMP(invalidTxRLP)

veryShortData:
        1                               :MSTORE(txCalldataLen)
        31 => D
                                        :CALL(SHLarith)
        A                               :MSTORE(SP++)
                                        :JMP(endData)

shortData:
        A - 0x80 => B                   :MSTORE(txCalldataLen)
                                        :JMP(readData)

longData:
        A - 0xb7 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        $ => D                          :MLOAD(batchHashPos)
        D                               :MSTORE(dataStarts)
        A => B                          :MSTORE(txCalldataLen)

readData:
        32 => D
        B - D                           :JMPN(readDataFinal)
        B - D                           :MSTORE(txDataRead)
                                        :CALL(addHashTx)
        A                               :MSTORE(SP++)
                                        :CALL(addBatchHashByteByByte)
        $ => B                          :MLOAD(txDataRead)
                                        :JMP(readData)

readDataFinal:
        B - 1                           :JMPN(endData)
        B => D
                                        :CALL(addHashTx)
        32 - D => D
                                        :CALL(SHLarith)
        A                               :MSTORE(SP)
        32 - D => D
                                        :CALL(addBatchHashByteByByte)

endData:


;; Read RLP 'chainId'
        ; 64 bits max
chainREAD:
        1 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        A - 0x80                        :JMPN(endChainId)
        A - 0x81                        :JMPN(chanId0)
        A - 0x89                        :JMPN(shortChainId)
                                        :JMP(invalidTxRLP)

chanId0:
        0 => A
                                        :JMPN(endChainId)

shortChainId:
        A - 0x80 => D
        D - 1                           :JMPN(endChainId)
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)

endChainId:
        A                               :MSTORE(txChainId)

;; Read RLP last two values (0, 0)
        ; 64 bits max
        2 => D
                                        :CALL(addHashTx)
                                        :CALL(addBatchHashData)
        0x8080 => B
        $                               :EQ,JMPC(sizeVerification)
                                        :JMP(invalidTxRLP)

;; size verification
        ; checks RLP lenght read at the RLP header with bytes read during RLP parsing
sizeVerification:
        C => A
        $ => B                          :MLOAD(txRLPLength)
        $                               :EQ,JMPC(sizeVerificationSuccess)
                                        :JMP(invalidTxRLP)
sizeVerificationSuccess:
        HASHPOS                         :HASHKLEN(E)

;;;;;;;;;;;;;;;;;;
;; C - Read signature. Fill 'batchHashData' bytes
;;;;;;;;;;;;;;;;;;

;; read ecdsa 'r'
rREADTx:
        32 => D
                                        :CALL(getTxBytes)
        A                               :MSTORE(txR)
        C + D => C
                                        :CALL(addBatchHashData)

;; read ecdsa 's'
sREADTx:
        32 => D
                                        :CALL(getTxBytes)
        A                               :MSTORE(txS)
        C + D => C
                                        :CALL(addBatchHashData)

;; read ecdsa 'v'
vREADTx:
        1 => D
                                        :CALL(getTxBytes)
        A                               :MSTORE(txV)
        C + D => C
                                        :CALL(addBatchHashData)

;;;;;;;;;
;; D - Finish RLP parsing
;;;;;;;;;

;; update bytes parsed
        $ => A                          :MLOAD(batchL2DataParsed)
        A + C => A                      :MSTORE(batchL2DataParsed)
;; increase number of transaction to process
        $ => A                          :MLOAD(pendingTxs)
        A + 1 => A                      :MSTORE(pendingTxs)
        HASHPOS                         :MSTORE(sigDataSize) ; save bytes length added to ethereum transaction hash
                                        :JMP(txLoopRLP)

;;;;;;;;;
;; E - Handler error RLP fields
;;;;;;;;;

invalidTxRLP:
;; Append all missing 'batchL2Data' to 'batchDataHash' bytes
        $ => B                          :MLOAD(batchL2DataLength)
        $ => C                          :MLOAD(batchHashPos)
        $ => HASHPOS                    :MLOAD(batchHashPos)
        $ => E                          :MLOAD(batchHashDataId)

appendTxs:
        B - C - 32                      :JMPN(finalAppendTxs)
        32 => D
        ${getTxs(p,D)} => A
        $${p = p + D}
        A                               :HASHK(E)
        C + D => C
                                        :JMP(appendTxs)

finalAppendTxs:
        B - C => D
        D - 1                           :JMPN(endAppendTxs)
        ${getTxs(p,D)} => A
        $${p = p + D}
        A                               :HASHK(E)
        C + D => C

endAppendTxs:
        HASHPOS                         :MSTORE(batchHashPos)
                                        :JMP(processTxsEnd)