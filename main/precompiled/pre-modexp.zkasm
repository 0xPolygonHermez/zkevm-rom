/**
 * @link [https://www.evm.codes/precompiled#0x05?fork=shanghai]
 * @zk-counters
 *  - dynamic steps:
 *  - dynamic arith:
 *  - dynamic binary:
 * @process-precompiled
 *  - stack input: [x1, y1, x2, y2]
 *  - stack output: [x, y]
 */
INCLUDE "../modexp/modexp.zkasm"
INCLUDE "../modexp/modexp_utils.zkasm"

INCLUDE "../modexp/array_lib/utils/array_is_zero.zkasm"
INCLUDE "../modexp/array_lib/utils/array_is_one.zkasm"
INCLUDE "../modexp/array_lib/utils/array_is_odd.zkasm"

INCLUDE "../modexp/array_lib/unused/array_sub_AGTB.zkasm"

INCLUDE "../modexp/array_lib/utils/array_trim.zkasm"
INCLUDE "../modexp/array_lib/utils/array_compare.zkasm"

INCLUDE "../modexp/array_lib/array_add_short.zkasm"
INCLUDE "../modexp/array_lib/array_add_AGTB.zkasm"
INCLUDE "../modexp/array_lib/array_add.zkasm"

; INCLUDE "array_mul_karatsuba.zkasm"
INCLUDE "../modexp/array_lib/array_mul_long.zkasm"
INCLUDE "../modexp/array_lib/array_mul_short.zkasm"
INCLUDE "../modexp/array_lib/array_mul.zkasm"
INCLUDE "../modexp/array_lib/array_square.zkasm"
INCLUDE "../modexp/array_lib/array_div_mod_long.zkasm"
INCLUDE "../modexp/array_lib/array_div_mod_short.zkasm"
INCLUDE "../modexp/array_lib/array_div_mod.zkasm"

VAR GLOBAL multiplication_complexity

VAR GLOBAL modexp_Bsize
VAR GLOBAL modexp_Esize
VAR GLOBAL modexp_Msize
VAR GLOBAL modexp_Einit
VAR GLOBAL modexp_Mend
VAR GLOBAL modexp_offset
VAR GLOBAL modexp_returnIndex

funcModexp:

    ;%MAX_CNT_BINARY - CNT_BINARY - X   :JMPN(outOfCountersBinary)
    ;%MAX_CNT_ARITH - CNT_ARITH - X     :JMPN(outOfCountersArith)
    ;%MAX_CNT_STEPS - STEP - X         :JMPN(outOfCountersStep)

    ; Move balances if value > 0 just before executing the contract CALL
    $ => B                              :MLOAD(txValue)
    0 => A
    zkPC+2 => RR
    $                                   :LT, JMPC(moveBalances)

    ; read data stored in argsOffsetCall
    ; Bsize [32 bytes], Esize [32 bytes], Msize [32 bytes]
    $ => CTX        :MLOAD(originCTX)
    $ => E          :MLOAD(argsOffsetCall)
                    :CALL(MLOAD32)
                    ${log(A,bsize)}
    A               :MSTORE(modexp_Bsize)           ;Bsize = base size
                    :CALL(MLOAD32)
                    ${log(A,esize)}
    A               :MSTORE(modexp_Esize)           ;Esize = exp size
                    :CALL(MLOAD32)
                    ${log(A,msize)}
    A               :MSTORE(modexp_Msize)           ;Msize = mod size
    E               :MSTORE(modexp_offset)
    $ => A          :MLOAD(modexp_Bsize)
    A               :MSTORE(arithA)
    E               :MSTORE(arithB),CALL(addARITH)
    $ => B          :MLOAD(arithRes1)
    B               :MSTORE(modexp_Einit)
    $ => B          :MLOAD(modexp_Msize)
    $               :LT, JMPC(calculateGas)
    A => B          :JMP(calculateGas)              ;B: max_length = max(Blen, Mlen)

/*
Gas

max_length = max(Bsize, Msize)
words = (max_length + 7) / 8
multiplication_complexity = words**2

iteration_count = 0
if Esize <= 32 and exponent == 0: iteration_count = 0
elif Esize <= 32: iteration_count = exponent.bit_length() - 1
elif Esize > 32: iteration_count = (8 * (Esize - 32)) + ((exponent & (2**256 - 1)).bit_length() - 1)
calculate_iteration_count = max(iteration_count, 1)

static_gas = 0
dynamic_gas = max(200, multiplication_complexity * iteration_count / 3)
 */

; E = iteration_count

VAR GLOBAL iteration_count
calculateGas:
    0 => E
    ; B = max(Bsize, Msize)
    B                                   :MSTORE(arithA)
    7                                   :MSTORE(arithB),CALL(addARITH)
    $ => B                              :MLOAD(arithRes1)
    B                                   :MSTORE(arithA)
    8                                   :MSTORE(arithB),CALL(divARITH)
    $ => C                              :MLOAD(arithRes1)   ;C: words = (max_length + 7) / 8
    C                                   :MSTORE(arithA)
    C                                   :MSTORE(arithB),CALL(mulARITH)
    $ => A                              :MLOAD(arithRes1)   ;A: multiplication_complexity = words**2
    A                                   :MSTORE(multiplication_complexity)

continueGas:
    $ => A                              :MLOAD(modexp_Esize)
    33 => B
    $ => B                              :LT,JMPC(modexp_expLT32)    ;if Esize <= 32 --> modexp_expLT32
    ;elif Esize > 32: iteration_count = (8 * (Esize - 32)) + ((exponent & (2**256 - 1)).bit_length() - 1)
    A                                   :MSTORE(arithA)
    32                                  :MSTORE(arithB),CALL(subARITH)
    $ => A                              :MLOAD(arithRes1)
    A                                   :MSTORE(arithA)
    8                                   :MSTORE(arithB),CALL(mulARITH)
    $ => A                              :MLOAD(arithRes1)   ; A = 8 * (Esize - 32)
    A                                   :MSTORE(iteration_count)    ;interation_count = 8 * (Esize - 32)
    $ => E                              :MLOAD(modexp_Einit)
                                        :CALL(MLOAD32)
    A => B                              :CALL(getLenBits)   ; A bits length
    A                                   :JMPZ(finalIterationCount)
    A - 1 => A

finalIterationCount:
    $ => B                              :MLOAD(iteration_count)
    A + B                               :MSTORE(iteration_count) ; iteration_count = (8 * (Esize - 32)) + ((exponent & (2**256 - 1)).bit_length() - 1)
    $ => E                              :MLOAD(iteration_count),JMP(finalGas) ; iteration_count = (8 * (Esize - 32)) + ((exponent & (2**256 - 1)).bit_length() - 1)

modexp_expLT32:
    $ => C                              :MLOAD(modexp_Esize)
    $ => E                              :MLOAD(modexp_Einit)
                                        :CALL(MLOADX)
    32 - C => D                         :CALL(SHRarith)
    A => B                              :CALL(getLenBits)    ;A = exponent.bit_length()
    A => E
    E                                   :JMPZ(finalGas)
    E - 1 => E                                              ;E = exponent.bit_length() - 1

finalGas:
    1 => A
    E => B
    $                                   :LT,JMPC(dinamicGas)
    1 => E

dinamicGas:
    ; E = calculate_iteration_count = max(iteration_count, 1)
    $ => A                              :MLOAD(multiplication_complexity)
    A                                   :MSTORE(arithA)
    E                                   :MSTORE(arithB),CALL(mulARITH)
    $ => A                              :MLOAD(arithRes1)   ; A = multiplication_complexity * iteration_count
    A                                   :MSTORE(arithA)
    3                                   :MSTORE(arithB),CALL(divARITH)
    $ => A                              :MLOAD(arithRes1)   ; A = multiplication_complexity * iteration_count / 3
    200 => B
    $                                   :LT,JMPC(callMODEXP)
    A => B

callMODEXP:
    ; B = max(200, multiplication_complexity * iteration_count / 3)
    $ => CTX                            :MLOAD(currentCTX)
    GAS - B => GAS                      :JMPN(outOfGas)
    $ => CTX                            :MLOAD(originCTX)
    0 => A
    $ => B                              :MLOAD(modexp_Msize)
    $                                   :EQ,JMPC(save0outMod0)
    $ => B                              :MLOAD(modexp_Bsize)
    $                                   :EQ,JMPC(save0out)
    %MAX_SIZE_MODEXP => A
    $ => B                              :MLOAD(modexp_Bsize)
    $                                   :LT,JMPC(endMODEXPFail)
    $ => B                              :MLOAD(modexp_Esize)
    $                                   :LT,JMPC(endMODEXPFail)
    $ => B                              :MLOAD(modexp_Msize)
    $                                   :LT,JMPC(endMODEXPFail)
    $ => E                              :MLOAD(modexp_offset)
    $ => C                              :MLOAD(modexp_Bsize)
                                        :CALL(modexp_getBase)
    $ => C                              :MLOAD(modexp_Esize)
                                        :CALL(modexp_getExp)
    $ => C                              :MLOAD(modexp_Msize)
    E                                   :MSTORE(arithA)
    C                                   :MSTORE(arithB),CALL(addARITH)
    $ => B                              :MLOAD(arithRes1)
    %MAX_SAFE_INTEGER_MODEXP => A
    $                                   :LT,JMPC(endMODEXPFail)
                                        :CALL(modexp_getMod)
    $ => A                              :MLOAD(modexp_Elen),JMPZ(modexpExp0)
    $ => A                              :MLOAD(modexp_Mlen),JMPZ(modexpMod0)
    $ => A                              :MLOAD(modexp_Blen),JMPZ(modexpBase0)
                                        :CALL(modexp)
                                        :JMP(finalMODEXP)

modexpBase0:
    $ => A                              :MLOAD(modexp_Elen)
    A                                   :JMPZ(save1out)
    0                                   :MSTORE(modexp_out),JMP(finalMODEXP)

modexpExp0:
modexpMod0:
    $ => A                              :MLOAD(modexp_Mlen)
    0                                   :MSTORE(modexp_out)
    A                                   :JMPZ(finalMODEXP)
    A - 1                               :JMPNZ(save1out)
    $ => A                              :MLOAD(modexp_M)
    A - 1                               :JMPZ(finalMODEXP)
save1out:
    1                                   :MSTORE(modexp_out),JMP(finalMODEXP)

save0out:
    $ => B                              :MLOAD(modexp_Msize)
    %MAX_SAFE_INTEGER_MODEXP => A
    ${log(A,aqui)}
    $                                   :LT,JMPC(endMODEXPFail)
    ${log(A,aquino)}
    0                                   :MSTORE(modexp_out),JMP(finalMODEXP)

save0outMod0:
    0                                   :MSTORE(modexp_out),JMP(endMODEXP)

finalMODEXP:
    $ => CTX                            :MLOAD(currentCTX)
    ; write data into memory
    0 => E
    $ => C                              :MLOAD(modexp_Msize)

    ; prepare return data
    0                                   :MSTORE(retDataOffset)
    C                                   :MSTORE(retDataLength)
    $ => B                              :MLOAD(retCallOffset)
    $ => A                              :MLOAD(originCTX),JMPZ(handleGas)
    ; set retDataCTX
    $ => E                              :MLOAD(currentCTX)
    A => CTX
    C                                   :MSTORE(arithA)
    32                                  :MSTORE(arithB),CALL(divARITH)
    $ => E                              :MLOAD(arithRes1)
    E                                   :MSTORE(modexp_returnIndex)
    $ => A                              :MLOAD(arithRes2),JMPZ(returnLoop)
    A => C
    $ => A                              :MLOAD(modexp_out+E)
    32 - C => D                         :CALL(SHLarith)
    A                                   :MSTORE(bytesToStore)
    B => E
    ${log(E,offset)}
                                        :CALL(MSTOREX) ; in: [bytesToStore, E: offset] out: [E: new offset]
    ${log(E,newoffset)}
    E => B
    $ => A                              :MLOAD(modexp_Msize)
    A - C                               :JMPZ(endMODEXP)
    $ => E                              :MLOAD(modexp_returnIndex)

returnLoop:
    E - 1 => E                          :MSTORE(modexp_returnIndex)
    ${log(E,index)}
    $ => A                              :MLOAD(modexp_out+E)
    A                                   :MSTORE(bytesToStore)
    ${log(A,bytes)}
    B => E
    ${log(E,offset)}
                                        :CALL(MSTORE32)
    E => B
    $ => E                              :MLOAD(modexp_returnIndex)
    C - 32 => C                         :JMPZ(endMODEXP, returnLoop)


endMODEXPFail:
    $ => CTX                            :MLOAD(currentCTX)
    $ => A                              :MLOAD(originCTX), JMPZ(handleGas)
    A => CTX
    0                                   :MSTORE(retDataCTX)
    0 => GAS
    CTX                                 :MSTORE(currentCTX), JMP(preEndFail)

preEndMODEXP:
    $ => CTX                            :MLOAD(originCTX)

endMODEXP:
    CTX                                 :MSTORE(currentCTX), JMP(preEnd)
