;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  halfPairingBN254:
;;          input: P âˆˆ G1 and Q âˆˆ G2
;;          output: It returns 1 if either P = ğ’ª or Q = ğ’ª and f_{r,Q}(P) âˆˆ FP2 otherwise
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

VAR GLOBAL halfPairingBN254_P_x
VAR GLOBAL halfPairingBN254_P_y
VAR GLOBAL halfPairingBN254_Q_x1
VAR GLOBAL halfPairingBN254_Q_x2
VAR GLOBAL halfPairingBN254_Q_y1
VAR GLOBAL halfPairingBN254_Q_y2

VAR GLOBAL halfPairingBN254_f11_x
VAR GLOBAL halfPairingBN254_f11_y
VAR GLOBAL halfPairingBN254_f12_x
VAR GLOBAL halfPairingBN254_f12_y
VAR GLOBAL halfPairingBN254_f13_x
VAR GLOBAL halfPairingBN254_f13_y
VAR GLOBAL halfPairingBN254_f21_x
VAR GLOBAL halfPairingBN254_f21_y
VAR GLOBAL halfPairingBN254_f22_x
VAR GLOBAL halfPairingBN254_f22_y
VAR GLOBAL halfPairingBN254_f23_x
VAR GLOBAL halfPairingBN254_f23_y

VAR GLOBAL halfPairingBN254_RR

VAR GLOBAL halfPairingBN254_P_x3
VAR GLOBAL halfPairingBN254_Q_RHS_x
VAR GLOBAL halfPairingBN254_Q_RHS_y
VAR GLOBAL halfPairingBN254_psi_x1
VAR GLOBAL halfPairingBN254_psi_x2
VAR GLOBAL halfPairingBN254_psi_y1
VAR GLOBAL halfPairingBN254_psi_y2

; ERROR CODES (B)
; 0 - no error
; 1 - P_x is too big
; 2 - P_y is too big
; 3 - Q_x1 is too big
; 4 - Q_x2 is too big
; 5 - Q_y1 is too big
; 6 - Q_y2 is too big
; 7 - P is not in G1
; 8 - Q is not in G2

; RESOURCES (not considering error paths):
; -------------------------------------------
; PATH 1 P == ğ’ª and Q == ğ’ª:     [steps: 40,  bin: 12, arith: 0]
; PATH 2 P == ğ’ª and Q != ğ’ª:     [steps: 20939, bin: 561, arith: 1804]
; PATH 3 P != ğ’ª and Q == ğ’ª:     [steps: 96,  bin: 17, arith: 8]
; PATH 4 P,Q != ğ’ª:              [steps: 172003, bin: 966, arith: 13327] <--- w.c.
; -------------------------------------------

halfPairingBN254:
    RR          :MSTORE(halfPairingBN254_RR)

    ; Check whether the points coordinates are in the range [0, BN254_P)
    %BN254_P_MINUS_ONE => A
    $ => B      :MLOAD(halfPairingBN254_P_x)
    $           :LT, JMPC(halfPairingBN254_Px_too_big)
    $ => B      :MLOAD(halfPairingBN254_P_y)
    $           :LT, JMPC(halfPairingBN254_Py_too_big)
    $ => B      :MLOAD(halfPairingBN254_Q_x1)
    $           :LT, JMPC(halfPairingBN254_Qx1_too_big)
    $ => B      :MLOAD(halfPairingBN254_Q_x2)
    $           :LT, JMPC(halfPairingBN254_Qx2_too_big)
    $ => B      :MLOAD(halfPairingBN254_Q_y1)
    $           :LT, JMPC(halfPairingBN254_Qy1_too_big)
    $ => B      :MLOAD(halfPairingBN254_Q_y2)
    $           :LT, JMPC(halfPairingBN254_Qy2_too_big)
    ; [steps: 14, bin: 6, arith: 0]

    ; Is P = ğ’ª?
    0n => B
    $ => A  :MLOAD(halfPairingBN254_P_x)
    $       :EQ, JMPNC(__halfPairingBN254_P_continue)
    $ => A  :MLOAD(halfPairingBN254_P_y)
    $       :EQ, JMPC(halfPairingBN254_P_is_zero)
            __halfPairingBN254_P_continue:
    ; [steps: 19, bin: 8, arith: 0]

    ; Is Q = ğ’ª?
    $ => A  :MLOAD(halfPairingBN254_Q_x1)
    $       :EQ, JMPNC(__halfPairingBN254_Q_continue1)
    $ => A  :MLOAD(halfPairingBN254_Q_x2)
    $       :EQ, JMPNC(__halfPairingBN254_Q_continue1)
    $ => A  :MLOAD(halfPairingBN254_Q_y1)
    $       :EQ, JMPNC(__halfPairingBN254_Q_continue1)
    $ => A  :MLOAD(halfPairingBN254_Q_y2)
    $       :EQ, JMPC(halfPairingBN254_Q_is_zero)
            __halfPairingBN254_Q_continue1:
            :JMP(halfPairingBN254_P_subgroup_check)
    ; [steps: 28, bin: 12, arith: 0]

; Begin of branching
halfPairingBN254_P_is_zero:
    ; before (w.c.) -> [steps: 19, bin: 8, arith: 0]

    ; Is Q = ğ’ª?
    $ => A  :MLOAD(halfPairingBN254_Q_x1)
    $       :EQ, JMPNC(__halfPairingBN254_Q_continue2)
    $ => A  :MLOAD(halfPairingBN254_Q_x2)
    $       :EQ, JMPNC(__halfPairingBN254_Q_continue2)
    $ => A  :MLOAD(halfPairingBN254_Q_y1)
    $       :EQ, JMPNC(__halfPairingBN254_Q_continue2)
    $ => A  :MLOAD(halfPairingBN254_Q_y2)
    $       :EQ, JMPC(halfPairingBN254_P_and_Q_are_zero)
            __halfPairingBN254_Q_continue2:
    ; [steps: 27, bin: 12, arith: 0]

    ; Check that Q âˆˆ G2
    ; Q in G2 iff Q in E' and psi(Q) == [6xÂ²]Q as proven in Proposition 3 of 2022/352

    ; 1] Check if Q âˆˆ E'(Fp2)
    ; Q in E' iff (Q.y1 + Q.y2Â·u)Â² == (Q.x1 + Q.x2Â·u)Â³ + 3/(9+u)
    ; 1.1] Compute LHS and RHS
    $ => A  :MLOAD(halfPairingBN254_Q_x1)
    $ => B  :MLOAD(halfPairingBN254_Q_x2), CALL(squareFp2BN254)
    ; E + CÂ·u = (Q.x1 + Q.x2Â·u)Â²
    ; [steps: 34, bin: 12, arith: 1]

    E => A
    C => B
    $ => C  :MLOAD(halfPairingBN254_Q_x1)
    $ => D  :MLOAD(halfPairingBN254_Q_x2), CALL(mulFp2BN254)
    ; E + CÂ·u = (Q.x1 + Q.x2Â·u)Â³
    ; [steps: 41, bin: 12, arith: 2]

    E => A
    C => B
    %BN254_ETWISTED_B_X => C
    %BN254_ETWISTED_B_Y => D     :CALL(addFp2BN254)
    ; E + CÂ·u = (Q.x1 + Q.x2Â·u)Â³ + 3/(9+u)
    E       :MSTORE(halfPairingBN254_Q_RHS_x)
    C       :MSTORE(halfPairingBN254_Q_RHS_y)
    ; [steps: 50, bin: 12, arith: 3]

    $ => A  :MLOAD(halfPairingBN254_Q_y1)
    $ => B  :MLOAD(halfPairingBN254_Q_y2), CALL(squareFp2BN254)
    ; E + CÂ·u = (Q.y1 + Q.y2Â·u)Â²
    ; [steps: 57, bin: 12, arith: 4]

    ; 1.2] Check if LHS == RHS
    E => A
    $ => B  :MLOAD(halfPairingBN254_Q_RHS_x)
    $       :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)

    C => A
    $ => B  :MLOAD(halfPairingBN254_Q_RHS_y)
    $       :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)
    ; [steps: 63, bin: 14, arith: 4]

    ; 2] Check if psi(Q) == [6xÂ²]Q
    ; 2.1] Compute psi(Q)
    %BN254_P => A
    $ => B      :MLOAD(halfPairingBN254_Q_x2)
    $ => D      :SUB                                ; D = -Qx2
    %FROBENIUS_GAMMA121 => A
    %FROBENIUS_GAMMA122 => B
    $ => C      :MLOAD(halfPairingBN254_Q_x1), CALL(mulFp2BN254)
    E           :MSTORE(halfPairingBN254_psi_x1)
    C           :MSTORE(halfPairingBN254_psi_x2)
    ; [steps: 74, bin: 15, arith: 5]

    %BN254_P => A
    $ => B      :MLOAD(halfPairingBN254_Q_y2)
    $ => D      :SUB                                ; D = -Qy2
    %FROBENIUS_GAMMA131 => A
    %FROBENIUS_GAMMA132 => B
    $ => C      :MLOAD(halfPairingBN254_Q_y1), CALL(mulFp2BN254)
    E           :MSTORE(halfPairingBN254_psi_y1)
    C           :MSTORE(halfPairingBN254_psi_y2)
    ; [steps: 85, bin: 16, arith: 6]

    ; 2.2] Compute [6xÂ²]Q
    $ => A      :MLOAD(halfPairingBN254_Q_x1)
    $ => B      :MLOAD(halfPairingBN254_Q_x2)
    $ => C      :MLOAD(halfPairingBN254_Q_y1)
    $ => D      :MLOAD(halfPairingBN254_Q_y2)
    A           :MSTORE(scalarMulPointBy6X2FP2BN254_P_x1)
    B           :MSTORE(scalarMulPointBy6X2FP2BN254_P_x2)
    C           :MSTORE(scalarMulPointBy6X2FP2BN254_P_y1)
    D           :MSTORE(scalarMulPointBy6X2FP2BN254_P_y2), CALL(scalarMulPointBy6X2FP2BN254)
    ; [steps: 20912, bin: 557, arith: 1804]

    ; 2.3] Check if psi(Q) == [6xÂ²]Q
    $ => A      :MLOAD(halfPairingBN254_psi_x1)
    $ => B      :MLOAD(scalarMulPointBy6X2FP2BN254_Q_x1)
    $           :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(halfPairingBN254_psi_x2)
    $ => B      :MLOAD(scalarMulPointBy6X2FP2BN254_Q_x2)
    $           :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(halfPairingBN254_psi_y1)
    $ => B      :MLOAD(scalarMulPointBy6X2FP2BN254_Q_y1)
    $           :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(halfPairingBN254_psi_y2)
    $ => B      :MLOAD(scalarMulPointBy6X2FP2BN254_Q_y2)
    $           :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)
    ; [steps: 20924, bin: 561, arith: 1804]

    ; e(O,Q) = 1
    1n      :MSTORE(halfPairingBN254_f11_x)
    0n      :MSTORE(halfPairingBN254_f11_y)
    0n      :MSTORE(halfPairingBN254_f12_x)
    0n      :MSTORE(halfPairingBN254_f12_y)
    0n      :MSTORE(halfPairingBN254_f13_x)
    0n      :MSTORE(halfPairingBN254_f13_y)
    0n      :MSTORE(halfPairingBN254_f21_x)
    0n      :MSTORE(halfPairingBN254_f21_y)
    0n      :MSTORE(halfPairingBN254_f22_x)
    0n      :MSTORE(halfPairingBN254_f22_y)
    0n      :MSTORE(halfPairingBN254_f23_x)
    0n      :MSTORE(halfPairingBN254_f23_y)

    0 => B     :JMP(halfPairingBN254_end)

    ; till the end -> [steps: 20939, bin: 561, arith: 1804]

halfPairingBN254_Q_is_zero:
    ; before (w.c.) -> [steps: 28, bin: 12, arith: 0]

    ; Check that P âˆˆ G1
    ; P in G1 iff (Py)Â² == (Px)Â³ + 3 (mod p)
    ; 1] Compute LHS and RHS
    $ => A,B    :MLOAD(halfPairingBN254_P_x), CALL(mulFpBN254); C = (Px)Â²
    C => A                                         ; A = (Px)Â²
    $ => B      :MLOAD(halfPairingBN254_P_x), CALL(mulFpBN254); C = (Px)Â³

    %BN254_E_B => A     :CALL(addFpBN254)                  ; C = (Px)Â³ + 3
    C           :MSTORE(halfPairingBN254_P_x3)               ; halfPairingBN254_P_x3 = (Px)Â³ + 3

    $ => A,B    :MLOAD(halfPairingBN254_P_y), CALL(mulFpBN254); C = (Py)Â²
    ; [steps: 78, bin: 16, arith: 8]

    ; 2] Check if LHS == RHS
    C => A
    $ => B      :MLOAD(halfPairingBN254_P_x3)
    $           :EQ, JMPNC(halfPairingBN254_P_is_not_in_G1)

    ; e(P,O) = 1
    1n      :MSTORE(halfPairingBN254_f11_x)
    0n      :MSTORE(halfPairingBN254_f11_y)
    0n      :MSTORE(halfPairingBN254_f12_x)
    0n      :MSTORE(halfPairingBN254_f12_y)
    0n      :MSTORE(halfPairingBN254_f13_x)
    0n      :MSTORE(halfPairingBN254_f13_y)
    0n      :MSTORE(halfPairingBN254_f21_x)
    0n      :MSTORE(halfPairingBN254_f21_y)
    0n      :MSTORE(halfPairingBN254_f22_x)
    0n      :MSTORE(halfPairingBN254_f22_y)
    0n      :MSTORE(halfPairingBN254_f23_x)
    0n      :MSTORE(halfPairingBN254_f23_y)

    0 => B     :JMP(halfPairingBN254_end)

    ; till the end -> [steps: 96, bin: 17, arith: 8]

halfPairingBN254_P_and_Q_are_zero:
    ; before (w.c.) -> [steps: 27, bin: 12, arith: 0]

    ; e(O,O) = 1
    1n      :MSTORE(halfPairingBN254_f11_x)
    0n      :MSTORE(halfPairingBN254_f11_y)
    0n      :MSTORE(halfPairingBN254_f12_x)
    0n      :MSTORE(halfPairingBN254_f12_y)
    0n      :MSTORE(halfPairingBN254_f13_x)
    0n      :MSTORE(halfPairingBN254_f13_y)
    0n      :MSTORE(halfPairingBN254_f21_x)
    0n      :MSTORE(halfPairingBN254_f21_y)
    0n      :MSTORE(halfPairingBN254_f22_x)
    0n      :MSTORE(halfPairingBN254_f22_y)
    0n      :MSTORE(halfPairingBN254_f23_x)
    0n      :MSTORE(halfPairingBN254_f23_y)

    0 => B     :JMP(halfPairingBN254_end)

    ; till the end -> [steps: 40, bin: 12, arith: 0]
; End of branching

halfPairingBN254_P_subgroup_check:
    ; before (w.c.) -> [steps: 28, bin: 12, arith: 0]

    ; Check that P âˆˆ G1
    ; P in G1 iff (Py)Â² == (Px)Â³ + 3 (mod p)
    ; 1] Compute LHS and RHS
    $ => A,B    :MLOAD(halfPairingBN254_P_x), CALL(mulFpBN254); C = (Px)Â²
    C => A                                         ; A = (Px)Â²
    $ => B      :MLOAD(halfPairingBN254_P_x), CALL(mulFpBN254); C = (Px)Â³

    %BN254_E_B => A     :CALL(addFpBN254)                  ; C = (Px)Â³ + 3
    C           :MSTORE(halfPairingBN254_P_x3)               ; halfPairingBN254_P_x3 = (Px)Â³ + 3

    $ => A,B    :MLOAD(halfPairingBN254_P_y), CALL(mulFpBN254); C = (Py)Â²
    ; [steps: 78, bin: 16, arith: 8]

    ; 2] Check if LHS == RHS
    C => A
    $ => B      :MLOAD(halfPairingBN254_P_x3)
    $           :EQ, JMPNC(halfPairingBN254_P_is_not_in_G1)
    ; [steps: 81, bin: 17, arith: 8]

halfPairingBN254_Q_subgroup_check:
    ; Check that Q âˆˆ G2
    ; Q in G2 iff Q in E' and psi(Q) == [6xÂ²]Q as proven in Proposition 3 of 2022/352

    ; 1] Check if Q âˆˆ E'(Fp2)
    ; Q in E' iff (Q.y1 + Q.y2Â·u)Â² == (Q.x1 + Q.x2Â·u)Â³ + 3/(9+u)
    ; 1.1] Compute LHS and RHS
    $ => A  :MLOAD(halfPairingBN254_Q_x1)
    $ => B  :MLOAD(halfPairingBN254_Q_x2), CALL(squareFp2BN254)
    ; E + CÂ·u = (Q.x1 + Q.x2Â·u)Â²

    E => A
    C => B
    $ => C  :MLOAD(halfPairingBN254_Q_x1)
    $ => D  :MLOAD(halfPairingBN254_Q_x2), CALL(mulFp2BN254)
    ; E + CÂ·u = (Q.x1 + Q.x2Â·u)Â³

    E => A
    C => B
    %BN254_ETWISTED_B_X => C
    %BN254_ETWISTED_B_Y => D :CALL(addFp2BN254)
    ; E + CÂ·u = (Q.x1 + Q.x2Â·u)Â³ + 3/(9+u)
    E           :MSTORE(halfPairingBN254_Q_RHS_x)
    C           :MSTORE(halfPairingBN254_Q_RHS_y)

    $ => A  :MLOAD(halfPairingBN254_Q_y1)
    $ => B  :MLOAD(halfPairingBN254_Q_y2), CALL(squareFp2BN254)
    ; E + CÂ·u = (Q.y1 + Q.y2Â·u)Â²

    ; 1.2] Check if LHS == RHS
    E => A
    $ => B  :MLOAD(halfPairingBN254_Q_RHS_x)
    $       :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)

    C => A
    $ => B  :MLOAD(halfPairingBN254_Q_RHS_y)
    $       :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)

    ; 2] Check if psi(Q) == [6xÂ²]Q
    ; 2.1] Compute psi(Q)
    %BN254_P => A
    $ => B      :MLOAD(halfPairingBN254_Q_x2)
    $ => D      :SUB                                ; D = -Qx2
    %FROBENIUS_GAMMA121 => A
    %FROBENIUS_GAMMA122 => B
    $ => C      :MLOAD(halfPairingBN254_Q_x1), CALL(mulFp2BN254)
    E           :MSTORE(halfPairingBN254_psi_x1)
    C           :MSTORE(halfPairingBN254_psi_x2)

    %BN254_P => A
    $ => B      :MLOAD(halfPairingBN254_Q_y2)
    $ => D      :SUB                                ; D = -Qx2
    %FROBENIUS_GAMMA131 => A
    %FROBENIUS_GAMMA132 => B
    $ => C      :MLOAD(halfPairingBN254_Q_y1), CALL(mulFp2BN254)
    E           :MSTORE(halfPairingBN254_psi_y1)
    C           :MSTORE(halfPairingBN254_psi_y2)

    ; 2.2] Compute [6xÂ²]Q
    $ => A      :MLOAD(halfPairingBN254_Q_x1)
    $ => B      :MLOAD(halfPairingBN254_Q_x2)
    $ => C      :MLOAD(halfPairingBN254_Q_y1)
    $ => D      :MLOAD(halfPairingBN254_Q_y2)
    A           :MSTORE(scalarMulPointBy6X2FP2BN254_P_x1)
    B           :MSTORE(scalarMulPointBy6X2FP2BN254_P_x2)
    C           :MSTORE(scalarMulPointBy6X2FP2BN254_P_y1)
    D           :MSTORE(scalarMulPointBy6X2FP2BN254_P_y2), CALL(scalarMulPointBy6X2FP2BN254)


    ; 2.3] Check if psi(Q) == [6xÂ²]Q
    $ => A      :MLOAD(halfPairingBN254_psi_x1)
    $ => B      :MLOAD(scalarMulPointBy6X2FP2BN254_Q_x1)
    $           :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(halfPairingBN254_psi_x2)
    $ => B      :MLOAD(scalarMulPointBy6X2FP2BN254_Q_x2)
    $           :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(halfPairingBN254_psi_y1)
    $ => B      :MLOAD(scalarMulPointBy6X2FP2BN254_Q_y1)
    $           :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(halfPairingBN254_psi_y2)
    $ => B      :MLOAD(scalarMulPointBy6X2FP2BN254_Q_y2)
    $           :EQ, JMPNC(halfPairingBN254_Q_is_not_in_G2)
    ; [steps: 20978, bin: 566, arith: 1812]

halfPairingBN254_Miller_loop:
    $ => A      :MLOAD(halfPairingBN254_P_x)
    $ => B      :MLOAD(halfPairingBN254_P_y)
    A           :MSTORE(millerLoopBN254_P_x)
    B           :MSTORE(millerLoopBN254_P_y)
    $ => A      :MLOAD(halfPairingBN254_Q_x1)
    $ => B      :MLOAD(halfPairingBN254_Q_x2)
    $ => C      :MLOAD(halfPairingBN254_Q_y1)
    $ => D      :MLOAD(halfPairingBN254_Q_y2)
    A           :MSTORE(millerLoopBN254_Q_x1)
    B           :MSTORE(millerLoopBN254_Q_x2)
    C           :MSTORE(millerLoopBN254_Q_y1)
    D           :MSTORE(millerLoopBN254_Q_y2), CALL(millerLoopBN254)
    ; [steps: 171976, bin: 966, arith: 13327]

    $ => A      :MLOAD(millerLoopBN254_f11_x)
    $ => B      :MLOAD(millerLoopBN254_f11_y)
    A           :MSTORE(halfPairingBN254_f11_x)
    B           :MSTORE(halfPairingBN254_f11_y)
    $ => A      :MLOAD(millerLoopBN254_f12_x)
    $ => B      :MLOAD(millerLoopBN254_f12_y)
    A           :MSTORE(halfPairingBN254_f12_x)
    B           :MSTORE(halfPairingBN254_f12_y)
    $ => A      :MLOAD(millerLoopBN254_f13_x)
    $ => B      :MLOAD(millerLoopBN254_f13_y)
    A           :MSTORE(halfPairingBN254_f13_x)
    B           :MSTORE(halfPairingBN254_f13_y)
    $ => A      :MLOAD(millerLoopBN254_f21_x)
    $ => B      :MLOAD(millerLoopBN254_f21_y)
    A           :MSTORE(halfPairingBN254_f21_x)
    B           :MSTORE(halfPairingBN254_f21_y)
    $ => A      :MLOAD(millerLoopBN254_f22_x)
    $ => B      :MLOAD(millerLoopBN254_f22_y)
    A           :MSTORE(halfPairingBN254_f22_x)
    B           :MSTORE(halfPairingBN254_f22_y)
    $ => A      :MLOAD(millerLoopBN254_f23_x)
    $ => B      :MLOAD(millerLoopBN254_f23_y)
    A           :MSTORE(halfPairingBN254_f23_x)
    B           :MSTORE(halfPairingBN254_f23_y)

    0 => B     :JMP(halfPairingBN254_end)

    ; till end -> [steps: 172003, bin: 966, arith: 13327]

; ERRORS
halfPairingBN254_Px_too_big:
    1 => B      :JMP(halfPairingBN254_error)

halfPairingBN254_Py_too_big:
    2 => B      :JMP(halfPairingBN254_error)

halfPairingBN254_Qx1_too_big:
    3 => B      :JMP(halfPairingBN254_error)

halfPairingBN254_Qx2_too_big:
    4 => B      :JMP(halfPairingBN254_error)

halfPairingBN254_Qy1_too_big:
    5 => B      :JMP(halfPairingBN254_error)

halfPairingBN254_Qy2_too_big:
    6 => B      :JMP(halfPairingBN254_error)

halfPairingBN254_P_is_not_in_G1:
    7 => B      :JMP(halfPairingBN254_error)

halfPairingBN254_Q_is_not_in_G2:
    8 => B      :JMP(halfPairingBN254_error)

halfPairingBN254_error:
    0 => A

halfPairingBN254_end:
    $ => RR     :MLOAD(halfPairingBN254_RR)
                :RETURN