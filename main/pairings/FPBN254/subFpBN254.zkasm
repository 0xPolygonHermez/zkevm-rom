;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; POST: The result is in the range [0,BN254_P)
;;
;; subFpBN254:
;;             in: A,C ∈ Fp
;;             out: C = A - C (mod BN254_P) ∈ Fp
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

subFpBN254:
        ; 1] Compute and check the sub over Z
        ; A·[1] + [BN254_P-C] = [D]·2²⁵⁶ + [E]
        1 => B
        ${const.BN254_P - C} => C
        $${var _subFpBN254_AC = A + C}
        ${_subFpBN254_AC >> 256} => D
        ${_subFpBN254_AC} => E :ARITH

        ; 2] Check it over Fp, that is, it must be satisfied that:
        ; [BN254_P]·[A+(BN254_P-C)] + [C] = D·2²⁵⁶ + E
        ; where C < BN254_P
        %BN254_P => A
        ${_subFpBN254_AC / const.BN254_P} => B        ; quotient  (256 bits)
        ${_subFpBN254_AC % const.BN254_P} => C        ; residue (256 bits)
        E :ARITH

        A => B
        C => A
        1       :LT, RETURN