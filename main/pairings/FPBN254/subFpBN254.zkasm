;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  PRE: A,C are assumed to be in the range [0,BN254_P)
;; POST: The result is in the range [0,BN254_P)
;;
;; subFpBN254:
;;             in: A,C ∈ Fp
;;             out: C = A - C (mod BN254_P) ∈ Fp
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; RESOURCES:
; -----------------------------
; PATH A >= C:  [steps: 3, bin: 1, arith: 0]
; PATH A < C:   [steps: 7, bin: 3, arith: 0] <-- w.c.
; -----------------------------

subFpBN254:
        C => B
        $ => C  :SUB, JMPC(subFpBN254_add_alias)
                :RETURN
subFpBN254_add_alias:
        ; if A < C, then we must compute A + BN254_P - C

        ; NOTE: 5·BN254_P < 2²⁵⁶ < 6·BN254_P

        B => C ; save for later use

        %BN254_P => B
        $ => A  :ADD ; It cannot overflow under the PRE condition

        C => B
        $ => C  :SUB, RETURN