;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; POST: The result is in the range [0,BN254_P)
;;
;; mulFpBN254:
;;             in: A,B ∈ Fp
;;             out: C = A·B (mod BN254_P) ∈ Fp
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; RESOURCES:
; -----------------------------
; [steps: 11, bin: 1, arith: 2]
; -----------------------------

mulFpBN254:
        ; 1] Compute and check the sum over Z
        ; A·B + [0] = [D]·2²⁵⁶ + [E]
        0 => C
        $${var _mulFpBN254_AB = A*B}
        ${_mulFpBN254_AB >> 256} => D
        ${_mulFpBN254_AB} => E :ARITH

        ; 2] Check it over Fp, that is, it must be satisfied that:
        ; [BN254_P]·[(A·B) / p] + [C / p] = D·2²⁵⁶ + E
        ; where C < BN254_P
        %BN254_P => A
        ${_mulFpBN254_AB / const.BN254_P} => B        ; quotient  (256 bits)
        ${_mulFpBN254_AB % const.BN254_P} => C        ; residue (256 bits)
        E :ARITH

        ; 3] Check that the result is lower than BN254_P
        A => B
        C => A
        1       :LT, RETURN