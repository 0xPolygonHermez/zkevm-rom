;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; POST: The result is in the range [0,BN254_P) because if falls back to FP2 arithmetic
;;
;; inverseFp6BN254:
;;             in: (a1 + a2·v + a3·v²) ∈ Fp6, where ai ∈ Fp2
;;             out: (c1 + c2·v + c3·v²) ∈ Fp6, where:
;;                  - c1 = (a1² - (9 + u)·(a2·a3))·(a1·c1mid + xi·(a3·c2mid + a2·c3mid))⁻¹
;;                  - c2 = ((9 + u)·a3² - (a1·a2))·(a1·c1mid + xi·(a3·c2mid + a2·c3mid))⁻¹
;;                  - c3 = (a2²-a1·a3)·(a1·c1mid + xi·(a3·c2mid + a2·c3mid))⁻¹
;;             with
;;                  * c1mid = a1² - (9 + u)·(a2·a3)
;;                  * c2mid = (9 + u)·a3² - (a1·a2)
;;                  * c3mid = a2² - (a1·a3)
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

VAR GLOBAL inverseFp6BN254_a1_x
VAR GLOBAL inverseFp6BN254_a1_y
VAR GLOBAL inverseFp6BN254_a2_x
VAR GLOBAL inverseFp6BN254_a2_y
VAR GLOBAL inverseFp6BN254_a3_x
VAR GLOBAL inverseFp6BN254_a3_y
VAR GLOBAL inverseFp6BN254_c1_x
VAR GLOBAL inverseFp6BN254_c1_y
VAR GLOBAL inverseFp6BN254_c2_x
VAR GLOBAL inverseFp6BN254_c2_y
VAR GLOBAL inverseFp6BN254_c3_x
VAR GLOBAL inverseFp6BN254_c3_y

VAR GLOBAL inverseFp6BN254_a1square_x
VAR GLOBAL inverseFp6BN254_a1square_y
VAR GLOBAL inverseFp6BN254_a2square_x
VAR GLOBAL inverseFp6BN254_a2square_y
VAR GLOBAL inverseFp6BN254_a3square_x
VAR GLOBAL inverseFp6BN254_a3square_y
VAR GLOBAL inverseFp6BN254_a1a2mul_x
VAR GLOBAL inverseFp6BN254_a1a2mul_y
VAR GLOBAL inverseFp6BN254_a1a3mul_x
VAR GLOBAL inverseFp6BN254_a1a3mul_y
VAR GLOBAL inverseFp6BN254_a2a3mul_x
VAR GLOBAL inverseFp6BN254_a2a3mul_y
VAR GLOBAL inverseFp6BN254_c1mid_x
VAR GLOBAL inverseFp6BN254_c1mid_y
VAR GLOBAL inverseFp6BN254_c2mid_x
VAR GLOBAL inverseFp6BN254_c2mid_y
VAR GLOBAL inverseFp6BN254_c3mid_x
VAR GLOBAL inverseFp6BN254_c3mid_y
VAR GLOBAL inverseFp6BN254_im_x
VAR GLOBAL inverseFp6BN254_im_y

VAR GLOBAL inverseFp6BN254_a3c2mid_x
VAR GLOBAL inverseFp6BN254_a3c2mid_y

VAR GLOBAL inverseFp6BN254_xia2c3mid_x
VAR GLOBAL inverseFp6BN254_xia2c3mid_y
VAR GLOBAL inverseFp6BN254_last_x
VAR GLOBAL inverseFp6BN254_last_y

VAR GLOBAL inverseFp6BN254_RR

; RESOURCES:
; -----------------------------
; PATH 1 a1·c1mid + xi·(a3·c2mid + a2·c3mid)_x == 0: [steps: 199, bin: 4, arith: 21]
; PATH 2 a1·c1mid + xi·(a3·c2mid + a2·c3mid)_x != 0: [steps: 202, bin: 3, arith: 22] <--- w.c.
; -----------------------------

inverseFp6BN254:
        RR              :MSTORE(inverseFp6BN254_RR)

        ; 1] a1²
        $ => A          :MLOAD(inverseFp6BN254_a1_x)
        $ => B          :MLOAD(inverseFp6BN254_a1_y), CALL(squareFp2BN254)
        E               :MSTORE(inverseFp6BN254_a1square_x)
        C               :MSTORE(inverseFp6BN254_a1square_y)

        ; 2] a2²
        $ => A          :MLOAD(inverseFp6BN254_a2_x)
        $ => B          :MLOAD(inverseFp6BN254_a2_y), CALL(squareFp2BN254)
        E               :MSTORE(inverseFp6BN254_a2square_x)
        C               :MSTORE(inverseFp6BN254_a2square_y)

        ; 3] a3²
        $ => A          :MLOAD(inverseFp6BN254_a3_x)
        $ => B          :MLOAD(inverseFp6BN254_a3_y), CALL(squareFp2BN254)
        E               :MSTORE(inverseFp6BN254_a3square_x)
        C               :MSTORE(inverseFp6BN254_a3square_y)

        ; 4] a1·a2
        $ => A          :MLOAD(inverseFp6BN254_a1_x)
        $ => B          :MLOAD(inverseFp6BN254_a1_y)
        $ => C          :MLOAD(inverseFp6BN254_a2_x)
        $ => D          :MLOAD(inverseFp6BN254_a2_y), CALL(mulFp2BN254)
        E               :MSTORE(inverseFp6BN254_a1a2mul_x)
        C               :MSTORE(inverseFp6BN254_a1a2mul_y)

        ; 5] a1·a3
        $ => A          :MLOAD(inverseFp6BN254_a1_x)
        $ => B          :MLOAD(inverseFp6BN254_a1_y)
        $ => C          :MLOAD(inverseFp6BN254_a3_x)
        $ => D          :MLOAD(inverseFp6BN254_a3_y), CALL(mulFp2BN254)
        E               :MSTORE(inverseFp6BN254_a1a3mul_x)
        C               :MSTORE(inverseFp6BN254_a1a3mul_y)

        ; 6] a2·a3
        $ => A          :MLOAD(inverseFp6BN254_a2_x)
        $ => B          :MLOAD(inverseFp6BN254_a2_y)
        $ => C          :MLOAD(inverseFp6BN254_a3_x)
        $ => D          :MLOAD(inverseFp6BN254_a3_y), CALL(mulFp2BN254)
        E               :MSTORE(inverseFp6BN254_a2a3mul_x)
        C               :MSTORE(inverseFp6BN254_a2a3mul_y)

        ; 7] c1mid = a1² - (9 + u)·(a2·a3)
        9n => A
        1n => B
        $ => C          :MLOAD(inverseFp6BN254_a2a3mul_x)
        $ => D          :MLOAD(inverseFp6BN254_a2a3mul_y), CALL(mulFp2BN254)
        $ => A          :MLOAD(inverseFp6BN254_a1square_x)
        $ => B          :MLOAD(inverseFp6BN254_a1square_y)
        C => D
        E => C          :CALL(subFp2BN254)

        E               :MSTORE(inverseFp6BN254_c1mid_x)
        C               :MSTORE(inverseFp6BN254_c1mid_y)

        ; 8] c2mid = (9 + u)·a3² - (a1·a2)
        9n => A
        1n => B
        $ => C          :MLOAD(inverseFp6BN254_a3square_x)
        $ => D          :MLOAD(inverseFp6BN254_a3square_y), CALL(mulFp2BN254)
        E => A
        C => B
        $ => C          :MLOAD(inverseFp6BN254_a1a2mul_x)
        $ => D          :MLOAD(inverseFp6BN254_a1a2mul_y), CALL(subFp2BN254)
        E               :MSTORE(inverseFp6BN254_c2mid_x)
        C               :MSTORE(inverseFp6BN254_c2mid_y)

        ; 9] c3mid = a2² - (a1·a3)
        $ => A          :MLOAD(inverseFp6BN254_a2square_x)
        $ => B          :MLOAD(inverseFp6BN254_a2square_y)
        $ => C          :MLOAD(inverseFp6BN254_a1a3mul_x)
        $ => D          :MLOAD(inverseFp6BN254_a1a3mul_y), CALL(subFp2BN254)
        E               :MSTORE(inverseFp6BN254_c3mid_x)
        C               :MSTORE(inverseFp6BN254_c3mid_y)

        ; 10] im = a1·c1mid
        $ => A          :MLOAD(inverseFp6BN254_a1_x)
        $ => B          :MLOAD(inverseFp6BN254_a1_y)
        $ => C          :MLOAD(inverseFp6BN254_c1mid_x)
        $ => D          :MLOAD(inverseFp6BN254_c1mid_y), CALL(mulFp2BN254)
        E               :MSTORE(inverseFp6BN254_im_x)
        C               :MSTORE(inverseFp6BN254_im_y)

        ; 11] last = (im + xi·(a3·c2mid + a2·c3mid))⁻¹
        $ => A          :MLOAD(inverseFp6BN254_a3_x)
        $ => B          :MLOAD(inverseFp6BN254_a3_y)
        $ => C          :MLOAD(inverseFp6BN254_c2mid_x)
        $ => D          :MLOAD(inverseFp6BN254_c2mid_y), CALL(mulFp2BN254)
        E               :MSTORE(inverseFp6BN254_a3c2mid_x)
        C               :MSTORE(inverseFp6BN254_a3c2mid_y)

        $ => A          :MLOAD(inverseFp6BN254_a2_x)
        $ => B          :MLOAD(inverseFp6BN254_a2_y)
        $ => C          :MLOAD(inverseFp6BN254_c3mid_x)
        $ => D          :MLOAD(inverseFp6BN254_c3mid_y), CALL(mulFp2BN254)

        $ => A          :MLOAD(inverseFp6BN254_a3c2mid_x)
        $ => B          :MLOAD(inverseFp6BN254_a3c2mid_y)
        C => D
        E => C          :CALL(addFp2BN254)

        9n => A
        1n => B
        C => D
        E => C          :CALL(mulFp2BN254)

        $ => A          :MLOAD(inverseFp6BN254_im_x)
        $ => B          :MLOAD(inverseFp6BN254_im_y)
        C => D
        E => C          :CALL(addFp2BN254)

        E => A
        C => B
        $ => C          :MLOAD(inverseFp6BN254_xia2c3mid_x)
        $ => D          :MLOAD(inverseFp6BN254_xia2c3mid_y), CALL(addFp2BN254)
        E => A
        C => B          :CALL(invFp2BN254)

        C               :MSTORE(inverseFp6BN254_last_x)
        D               :MSTORE(inverseFp6BN254_last_y)

        ; 12] c1 = c1mid·last, c2 = c2mid·last, c3 = c3mid·last
        $ => A          :MLOAD(inverseFp6BN254_c1mid_x)
        $ => B          :MLOAD(inverseFp6BN254_c1mid_y)
        $ => C          :MLOAD(inverseFp6BN254_last_x)
        $ => D          :MLOAD(inverseFp6BN254_last_y), CALL(mulFp2BN254)
        E               :MSTORE(inverseFp6BN254_c1_x)
        C               :MSTORE(inverseFp6BN254_c1_y)

        $ => A          :MLOAD(inverseFp6BN254_c2mid_x)
        $ => B          :MLOAD(inverseFp6BN254_c2mid_y)
        $ => C          :MLOAD(inverseFp6BN254_last_x)
        $ => D          :MLOAD(inverseFp6BN254_last_y), CALL(mulFp2BN254)
        E               :MSTORE(inverseFp6BN254_c2_x)
        C               :MSTORE(inverseFp6BN254_c2_y)

        $ => A          :MLOAD(inverseFp6BN254_c3mid_x)
        $ => B          :MLOAD(inverseFp6BN254_c3mid_y)
        $ => C          :MLOAD(inverseFp6BN254_last_x)
        $ => D          :MLOAD(inverseFp6BN254_last_y), CALL(mulFp2BN254)
        E               :MSTORE(inverseFp6BN254_c3_x)
        C               :MSTORE(inverseFp6BN254_c3_y)

        $ => RR         :MLOAD(inverseFp6BN254_RR)
                        :RETURN