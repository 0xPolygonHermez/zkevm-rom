;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  PRE: Both P and 2Â·P are in E(Fp)\{ð’ª} and its coordinates are in the range [0,BN254_P)
;; POST: The resulting coordinates are in the range [0,BN254_P) because if falls back to FP arithmetic
;;
;; doublePointFPBN254:
;;             in: P = (P.x, P.y) âˆˆ E(Fp)
;;             out: 2Â·P = (Q.x, Q.y) âˆˆ E(Fp)
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Since the curve is E/Fp: yÂ² = xÂ³ + 3, there is no issue in representing the point at infinity as (0, 0).

VAR GLOBAL doublePointFPBN254_P_x
VAR GLOBAL doublePointFPBN254_P_y
VAR GLOBAL doublePointFPBN254_P_y_inv
VAR GLOBAL doublePointFPBN254_Q_x
VAR GLOBAL doublePointFPBN254_Q_y
VAR GLOBAL doublePointFPBN254_lambda

VAR GLOBAL doublePointFPBN254_RR

; RESOURCES:
; -----------------------------
; [steps: 174, bin: 14, arith: 24]
; -----------------------------

doublePointFPBN254:
        RR      :MSTORE(doublePointFPBN254_RR)

        $ => A  :MLOAD(doublePointFPBN254_P_y)
        $ => C  :MLOAD(doublePointFPBN254_P_y), CALL(addFpBN254)
        ; C = 2y
        ; [steps: 14, bin: 1, arith: 2]

        C => A  :CALL(invFpBN254)
        ; B = 1 / 2y
        B       :MSTORE(doublePointFPBN254_P_y_inv)
        ; [steps: 36, bin: 4, arith: 4]

        B => A,C :CALL(addFpBN254)
        C => A
        $ => C  :MLOAD(doublePointFPBN254_P_y_inv), CALL(addFpBN254)
        ; C = 3/2y
        ; [steps: 61, bin: 6, arith: 8]

        C => A
        $ => B  :MLOAD(doublePointFPBN254_P_x), CALL(mulFpBN254)
        ; C = 3x/2y

        C => A
        $ => B  :MLOAD(doublePointFPBN254_P_x), CALL(mulFpBN254)
        ; C = lambda = 3xÂ²/2y
        ; [steps: 87, bin: 8, arith: 12]

        C       :MSTORE(doublePointFPBN254_lambda)
        ; C = lambda

        C => A  :CALL(squareFpBN254)
        ; B = lambdaÂ²
        ; [steps: 102, bin: 9, arith: 14]

        B => A
        $ => C  :MLOAD(doublePointFPBN254_P_x), CALL(subFpBN254)
        ; C = lambdaÂ² - P.x

        C => A
        $ => C  :MLOAD(doublePointFPBN254_P_x), CALL(subFpBN254)
        ; C = lambdaÂ² - P.x - P.x

        C           :MSTORE(doublePointFPBN254_Q_x)
        ; Q.x = lambdaÂ² - P.x - P.x

        $ => A  :MLOAD(doublePointFPBN254_P_x), CALL(subFpBN254)
        ; C = P.x - Q.x
        ; [steps: 144, bin: 12, arith: 20]

        $ => A  :MLOAD(doublePointFPBN254_lambda)
        C => B  :CALL(mulFpBN254)
        ; C = lambdaÂ·(P.x - Q.x)
        ; [steps: 157, bin: 13, arith: 22]

        C => A
        $ => C  :MLOAD(doublePointFPBN254_P_y), CALL(subFpBN254)
        ; C = lambdaÂ·(P.x - Q.x) - P.y

        C       :MSTORE(doublePointFPBN254_Q_y)
        ; Q.y = lambdaÂ·(P.x - Q.x) - P.y

doublePointFPBN254_end:
        $ => RR         :MLOAD(doublePointFPBN254_RR)
                        :RETURN

        ; [steps: 174, bin: 14, arith: 24]