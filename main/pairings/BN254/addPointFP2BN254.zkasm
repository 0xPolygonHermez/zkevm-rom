;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; PRE: P1,P2 ∈ E'(Fp2)
;; POST: The resulting coordinates are in the range [0,BN254_P) because if falls back to FP2 arithmetic
;;
;; addPointFP2BN254:
;;             in: P1 = (P1.x1 + P1.x2·u, P1.y1 + P1.y2·u), P2 = (P2.x1 + P2.x2·u, P2.y1 + P2.y2·u) ∈ E'(Fp2)
;;             out: P1 + P2 = (P3.x1 + P3.x2·u, P3.y1 + P3.y2·u) ∈ E'(Fp2)
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; addPointFP2BN254 assumes both P1 and P2 belong to E'(Fp2), since it is checked in the pairing.
; However, it must be implemented if addPointFP2BN254 wants to be used independently.

; Since the curve is E'/Fp2: y² = x³ + 3/(9+u), there is no issue in representing the point at infinity as (0, 0).

VAR GLOBAL addPointFP2BN254_P1_x1
VAR GLOBAL addPointFP2BN254_P1_x2
VAR GLOBAL addPointFP2BN254_P1_y1
VAR GLOBAL addPointFP2BN254_P1_y2
VAR GLOBAL addPointFP2BN254_P2_x1
VAR GLOBAL addPointFP2BN254_P2_x2
VAR GLOBAL addPointFP2BN254_P2_y1
VAR GLOBAL addPointFP2BN254_P2_y2
VAR GLOBAL addPointFP2BN254_P3_x1
VAR GLOBAL addPointFP2BN254_P3_x2
VAR GLOBAL addPointFP2BN254_P3_y1
VAR GLOBAL addPointFP2BN254_P3_y2
VAR GLOBAL addPointFP2BN254_lambda_x
VAR GLOBAL addPointFP2BN254_lambda_y
VAR GLOBAL addPointFP2BN254_RR

; RESOURCES (not considering error paths):
; -------------------------------------------
; PATH 1 P1 == 𝒪:                     [steps: 20, bin: 4]
; PATH 2 P1 != 𝒪 and P2 == 𝒪:         [steps: 29, bin: 8]
; PATH 3 P1,P2 != 𝒪 and P1 == -P2:    [steps: 46, bin: 14]
; PATH 4 P1,P2 != 𝒪 and P1 == P2:     [steps: 119, bin: 10, arith: 11]-[steps: 131, bin: 16, arith: 11] <--- w.c.
; PATH 5 P1,P2 != 𝒪 and P1 != P2,-P2: [steps: 109, bin: 8,  arith: 10]-[steps: 121, bin: 14, arith: 10]
; -------------------------------------------

addPointFP2BN254:
        RR      :MSTORE(addPointFP2BN254_RR)

        ; Is P1 = 𝒪?
        0n => B
        $ => A  :MLOAD(addPointFP2BN254_P1_x1)
        $       :EQ, JMPNC(__addPointFP2BN254_P1_continue)
        $ => A  :MLOAD(addPointFP2BN254_P1_x2)
        $       :EQ, JMPNC(__addPointFP2BN254_P1_continue)
        $ => A  :MLOAD(addPointFP2BN254_P1_y1)
        $       :EQ, JMPNC(__addPointFP2BN254_P1_continue)
        $ => A  :MLOAD(addPointFP2BN254_P1_y2)
        $       :EQ, JMPC(addPointFP2BN254_P1_is_zero)
                __addPointFP2BN254_P1_continue:
        ; [steps: 10, bin: 4]

        ; Is P2 = 𝒪?
        0n => B
        $ => A  :MLOAD(addPointFP2BN254_P2_x1)
        $       :EQ, JMPNC(__addPointFP2BN254_P2_continue)
        $ => A  :MLOAD(addPointFP2BN254_P2_x2)
        $       :EQ, JMPNC(__addPointFP2BN254_P2_continue)
        $ => A  :MLOAD(addPointFP2BN254_P2_y1)
        $       :EQ, JMPNC(__addPointFP2BN254_P2_continue)
        $ => A  :MLOAD(addPointFP2BN254_P2_y2)
        $       :EQ, JMPC(addPointFP2BN254_P2_is_zero)
                __addPointFP2BN254_P2_continue:
        ; [steps: 19, bin: 8]

        ; P1 and P2 are not 𝒪, let's check whether they are different points, the same point or inverses of each other
        ; Is P1.x == P2.x?
        $ => A  :MLOAD(addPointFP2BN254_P1_x1)
        $ => B  :MLOAD(addPointFP2BN254_P2_x1)
        $       :EQ, JMPNC(addPointFP2BN254_different)
        $ => A  :MLOAD(addPointFP2BN254_P1_x2)
        $ => B  :MLOAD(addPointFP2BN254_P2_x2)
        $       :EQ, JMPNC(addPointFP2BN254_different)
        ; [steps: 25, bin: 10]

        ; Is P1.y == P2.y?
        $ => A  :MLOAD(addPointFP2BN254_P1_y1)
        $ => B  :MLOAD(addPointFP2BN254_P2_y1)
        $       :EQ, JMPNC(addPointFP2BN254_P1_and_P2_are_inverted)
        $ => A  :MLOAD(addPointFP2BN254_P1_y2)
        $ => B  :MLOAD(addPointFP2BN254_P2_y2)
        $       :EQ, JMPC(addPointFP2BN254_same, addPointFP2BN254_P1_and_P2_are_inverted)
        ; [steps: 31, bin: 12]

; Begin of branching
addPointFP2BN254_P1_is_zero:
        ; before (w.c.) -> [steps: 10, bin: 4]

        ; P3 = P2
        $ => A  :MLOAD(addPointFP2BN254_P2_x1)
        $ => B  :MLOAD(addPointFP2BN254_P2_x2)
        $ => C  :MLOAD(addPointFP2BN254_P2_y1)
        $ => D  :MLOAD(addPointFP2BN254_P2_y2)
        A       :MSTORE(addPointFP2BN254_P3_x1)
        B       :MSTORE(addPointFP2BN254_P3_x2)
        C       :MSTORE(addPointFP2BN254_P3_y1)
        D       :MSTORE(addPointFP2BN254_P3_y2), JMP(addPointFP2BN254_end)

        ; till the end -> [steps: 20, bin: 4]

addPointFP2BN254_P2_is_zero:
        ; before (w.c.) -> [steps: 19, bin: 8]

        ; P3 = P1
        $ => A  :MLOAD(addPointFP2BN254_P1_x1)
        $ => B  :MLOAD(addPointFP2BN254_P1_x2)
        $ => C  :MLOAD(addPointFP2BN254_P1_y1)
        $ => D  :MLOAD(addPointFP2BN254_P1_y2)
        A       :MSTORE(addPointFP2BN254_P3_x1)
        B       :MSTORE(addPointFP2BN254_P3_x2)
        C       :MSTORE(addPointFP2BN254_P3_y1)
        D       :MSTORE(addPointFP2BN254_P3_y2), JMP(addPointFP2BN254_end)

        ; till the end -> [steps: 29, bin: 8]

addPointFP2BN254_P1_and_P2_are_inverted:
        ; before (w.c.) -> [steps: 31, bin: 12]

        ; Check -P1.y == P2.y
        %BN254_P => A
        $ => B          :MLOAD(addPointFP2BN254_P1_y1)
        $ => C          :SUB
        $ => B          :MLOAD(addPointFP2BN254_P1_y2)
        $ => D          :SUB

        $ => A          :MLOAD(addPointFP2BN254_P2_y1)
        C               :ASSERT
        $ => A          :MLOAD(addPointFP2BN254_P2_y2)
        D               :ASSERT

        ; P3 = 𝒪
        0n      :MSTORE(addPointFP2BN254_P3_x1)
        0n      :MSTORE(addPointFP2BN254_P3_x2)
        0n      :MSTORE(addPointFP2BN254_P3_y1)
        0n      :MSTORE(addPointFP2BN254_P3_y2), JMP(addPointFP2BN254_end)

        ; till the end -> [steps: 46, bin: 14]
; End of branching

addPointFP2BN254_same:
        ; before (w.c.) -> [steps: 31, bin: 12]

        $ => A  :MLOAD(addPointFP2BN254_P1_y1)
        $ => B  :MLOAD(addPointFP2BN254_P1_y2)
        $ => C  :MLOAD(addPointFP2BN254_P1_y1)
        $ => D  :MLOAD(addPointFP2BN254_P1_y2), CALL(addFp2BN254)
        ; E + C·u = 2y
        ; [steps: 38, bin: 12, arith: 1]

        E => A
        C => B  :CALL(invFp2BN254)
        ; C + D·u = 1 / 2y
        ; [steps: 63, bin: 16, arith: 2]

        3n => A :CALL(scalarMulFp2BN254)
        ; E + C·u = 3/2y
        ; [steps: 68, bin: 16, arith: 3]

        $ => A  :MLOAD(addPointFP2BN254_P1_x1)
        $ => B  :MLOAD(addPointFP2BN254_P1_x2)
        C => D
        E => C  :CALL(mulFp2BN254)
        ; E + C·u = 3x/2y

        $ => A  :MLOAD(addPointFP2BN254_P1_x1)
        $ => B  :MLOAD(addPointFP2BN254_P1_x2)
        C => D
        E => C  :CALL(mulFp2BN254)
        ; E + C·u = lambda = 3x²/2y
        ; [steps: 82, bin: 16, arith: 5]

        E       :MSTORE(addPointFP2BN254_lambda_x)
        C       :MSTORE(addPointFP2BN254_lambda_y)
        ; E + C·u = lambda

        E => A
        C => B  :CALL(squareFp2BN254)
        ; E + C·u = lambda²
        ; [steps: 91, bin: 16, arith: 6]

        E => A
        C => B
        $ => C  :MLOAD(addPointFP2BN254_P1_x1)
        $ => D  :MLOAD(addPointFP2BN254_P1_x2), CALL(subFp2BN254)
        ; E + C·u = lambda² - x

        E => A
        C => B
        $ => C  :MLOAD(addPointFP2BN254_P1_x1)
        $ => D  :MLOAD(addPointFP2BN254_P1_x2), CALL(subFp2BN254)
        ; E + C·u = lambda² - x - x

                :JMP(addPointFP2BN254_common_calculate)

        ; [steps: 106, bin: 16, arith: 8]

addPointFP2BN254_different:
        ; before (w.c.) ->  [steps: 25, bin: 10]

        $ => A  :MLOAD(addPointFP2BN254_P2_x1)
        $ => B  :MLOAD(addPointFP2BN254_P2_x2)
        $ => C  :MLOAD(addPointFP2BN254_P1_x1)
        $ => D  :MLOAD(addPointFP2BN254_P1_x2), CALL(subFp2BN254)
        ; E + C·u = P2.x - P1.x
        ; [steps: 32, bin: 10, arith: 1]

        E => A
        C => B  :CALL(invFp2BN254)
        ; C + D·u = 1 / (P2_x - P1_x)
        C       :MSTORE(addPointFP2BN254_lambda_x)
        D       :MSTORE(addPointFP2BN254_lambda_y)
        ; [steps: 59, bin: 14, arith: 2]

        $ => A  :MLOAD(addPointFP2BN254_P2_y1)
        $ => B  :MLOAD(addPointFP2BN254_P2_y2)
        $ => C  :MLOAD(addPointFP2BN254_P1_y1)
        $ => D  :MLOAD(addPointFP2BN254_P1_y2), CALL(subFp2BN254)
        ; E + C·u = P2.y - P1.y
        ; [steps: 66, bin: 14, arith: 3]

        $ => A  :MLOAD(addPointFP2BN254_lambda_x)
        $ => B  :MLOAD(addPointFP2BN254_lambda_y)
        C => D
        E => C  :CALL(mulFp2BN254)
        ; E + C·u = lambda = (P2_y - P1_y) / (P2_x - P1_x)
        E       :MSTORE(addPointFP2BN254_lambda_x)
        C       :MSTORE(addPointFP2BN254_lambda_y)
        ; [steps: 75, bin: 14, arith: 4]

        E => A
        C => B  :CALL(squareFp2BN254)
        ; E + C·u = lambda²
        ; [steps: 82, bin: 14, arith: 5]

        E => A
        C => B
        $ => C  :MLOAD(addPointFP2BN254_P1_x1)
        $ => D  :MLOAD(addPointFP2BN254_P1_x2), CALL(subFp2BN254)
        ; E + C·u = lambda² - P1.x

        E => A
        C => B
        $ => C  :MLOAD(addPointFP2BN254_P2_x1)
        $ => D  :MLOAD(addPointFP2BN254_P2_x2), CALL(subFp2BN254)
        ; E + C·u = lambda² - P1.x - P2.x
        ; [steps: 96, bin: 14, arith: 7]

addPointFP2BN254_common_calculate:
        ; block costs: [steps: 25, bin: 0, arith: 3]

        E           :MSTORE(addPointFP2BN254_P3_x1)
        C           :MSTORE(addPointFP2BN254_P3_x2)
        ; P3.x = lambda² - P1.x - P2.x

        $ => A  :MLOAD(addPointFP2BN254_P1_x1)
        $ => B  :MLOAD(addPointFP2BN254_P1_x2)
        C => D
        E => C  :CALL(subFp2BN254)
        ; E + C·u = P1.x - P3.x
        ; [steps: 9, bin: 0, arith: 1]

        $ => A  :MLOAD(addPointFP2BN254_lambda_x)
        $ => B  :MLOAD(addPointFP2BN254_lambda_y)
        C => D
        E => C  :CALL(mulFp2BN254)
        ; E + C·u = lambda·(P1.x - P3.x)
        ; [steps: 16, bin: 0, arith: 2]

        E => A
        C => B
        $ => C  :MLOAD(addPointFP2BN254_P1_y1)
        $ => D  :MLOAD(addPointFP2BN254_P1_y2), CALL(subFp2BN254)
        ; E + C·u = lambda·(P1.x - P3.x) - P1.y

        E           :MSTORE(addPointFP2BN254_P3_y1)
        C           :MSTORE(addPointFP2BN254_P3_y2)

        ; till the end -> [steps: 25, bin: 0, arith: 3]

addPointFP2BN254_end:
        $ => RR         :MLOAD(addPointFP2BN254_RR)
                        :RETURN