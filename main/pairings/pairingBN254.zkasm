;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Optimal Ate Pairing e: G1 x G2 -> GT over the BN254 curve
;; where G1 = E(Fp)[r] = E(Fp), G2 = E'(Fp2)[r] and GT = mu_r (the r-th roots of unity over (Fp12)^*)
;; the involved curves are E/Fp: y² = x³ + 3 and E'/Fp2: y² = x³ + 3/(9+u)
;;  pairingBN254:
;;          input: P ∈ G1 and Q ∈ G2
;;          output: e(P,Q) ∈ GT
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

VAR GLOBAL pairingBN254_P_x
VAR GLOBAL pairingBN254_P_y
VAR GLOBAL pairingBN254_Q_x1
VAR GLOBAL pairingBN254_Q_x2
VAR GLOBAL pairingBN254_Q_y1
VAR GLOBAL pairingBN254_Q_y2

VAR GLOBAL pairingBN254_f11_x
VAR GLOBAL pairingBN254_f11_y
VAR GLOBAL pairingBN254_f12_x
VAR GLOBAL pairingBN254_f12_y
VAR GLOBAL pairingBN254_f13_x
VAR GLOBAL pairingBN254_f13_y
VAR GLOBAL pairingBN254_f21_x
VAR GLOBAL pairingBN254_f21_y
VAR GLOBAL pairingBN254_f22_x
VAR GLOBAL pairingBN254_f22_y
VAR GLOBAL pairingBN254_f23_x
VAR GLOBAL pairingBN254_f23_y

VAR GLOBAL pairingBN254_RR

VAR GLOBAL pairingBN254_P_x3
VAR GLOBAL pairingBN254_Q_RHS_x
VAR GLOBAL pairingBN254_Q_RHS_y
VAR GLOBAL pairingBN254_psi_x1
VAR GLOBAL pairingBN254_psi_x2
VAR GLOBAL pairingBN254_psi_y1
VAR GLOBAL pairingBN254_psi_y2 

INCLUDE "constants.zkasm"

; ERROR CODES (B)
; 0 - no error
; 1 - P is not in G1
; 2 - Q is not in G2

pairingBN254:
    RR          :MSTORE(pairingBN254_RR)

    ; Check if P = (0,0)
    ${FpBN254neq0(mem.pairingBN254_P_x) || FpBN254neq0(mem.pairingBN254_P_y)}   :JMPZ(pairingBN254_P_is_zero)

    ; Check if Q = (0,0)
    ${FpBN254neq0(mem.pairingBN254_Q_x1) || FpBN254neq0(mem.pairingBN254_Q_x2) || FpBN254neq0(mem.pairingBN254_Q_y1) || FpBN254neq0(mem.pairingBN254_Q_y2)}   :JMPZ(pairingBN254_Q_is_zero)

                :JMP(pairingBN254_P_subgroup_check)


pairingBN254_P_is_zero:
    ; Check that P is zero
    0n      :MLOAD(pairingBN254_P_x)
    0n      :MLOAD(pairingBN254_P_y)

    ; Check that Q is in G2
    ; Q in G2 iff Q in E' and psi(Q) == [6x²]Q as proven in Proposition 3 of 2022/352

    ; 1] Check if Q is in E'(Fp2)
    ; Q in E' iff (Q.y1 + Q.y2·u)² == (Q.x1 + Q.x2·u)³ + 3/(9+u)
    ; 1.1] Compute LHS and RHS
    $ => A  :MLOAD(pairingBN254_Q_x1)
    $ => B  :MLOAD(pairingBN254_Q_x2)
            :CALL(squareFp2BN254)
    ; E + C·u = (Q.x1 + Q.x2·u)²

    E => A
    C => B
    $ => C  :MLOAD(pairingBN254_Q_x1)
    $ => D  :MLOAD(pairingBN254_Q_x2)
            :CALL(mulFp2BN254)
    ; E + C·u = (Q.x1 + Q.x2·u)³

    E => A
    C => B
    %BN254_ETWISTED_B_X => C
    %BN254_ETWISTED_B_Y => D
            :CALL(addFp2BN254)
    ; E + C·u = (Q.x1 + Q.x2·u)³ + 3/(9+u)
    E           :MSTORE(pairingBN254_Q_RHS_x)
    C           :MSTORE(pairingBN254_Q_RHS_y)

    $ => A  :MLOAD(pairingBN254_Q_y1)
    $ => B  :MLOAD(pairingBN254_Q_y2)
            :CALL(squareFp2BN254)
    ; E + C·u = (Q.y1 + Q.y2·u)²

    ; 1.2] Check if LHS == RHS
    E => A
    $ => B  :MLOAD(pairingBN254_Q_RHS_x)
    $       :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

    C => A
    $ => B  :MLOAD(pairingBN254_Q_RHS_y)
    $       :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

    ; 2] Compute psi(Q)                   
    %BN254_P => A
    $ => B      :MLOAD(pairingBN254_Q_x2)             
    $ => D      :SUB                                ; D = -Qx2
    %FROBENIUS_GAMMA121 => A
    %FROBENIUS_GAMMA122 => B
    $ => C      :MLOAD(pairingBN254_Q_x1), CALL(mulFp2BN254)
    E           :MSTORE(pairingBN254_psi_x1)
    C           :MSTORE(pairingBN254_psi_x2)

    %BN254_P => A
    $ => B      :MLOAD(pairingBN254_Q_y2)             
    $ => D      :SUB                                ; D = -Qy2
    %FROBENIUS_GAMMA131 => A
    %FROBENIUS_GAMMA132 => B
    $ => C      :MLOAD(pairingBN254_Q_y1), CALL(mulFp2BN254)
    E           :MSTORE(pairingBN254_psi_y1)
    C           :MSTORE(pairingBN254_psi_y2)

    ; 3] Compute [6x²]Q (here, we check that Q is in E')
    $ => A      :MLOAD(pairingBN254_Q_x1)
    $ => B      :MLOAD(pairingBN254_Q_x2)
    $ => C      :MLOAD(pairingBN254_Q_y1)
    $ => D      :MLOAD(pairingBN254_Q_y2)
    A           :MSTORE(escalarMulBN254_P_x1)
    B           :MSTORE(escalarMulBN254_P_x2)
    C           :MSTORE(escalarMulBN254_P_y1)
    D           :MSTORE(escalarMulBN254_P_y2)
    147946756881789318990833708069417712966n :MSTORE(escalarMulBN254_k) ; 6x²
                :CALL(escalarMulBN254)


    ; 4] Check if psi(Q) == [6x²]Q
    $ => A      :MLOAD(pairingBN254_psi_x1)
    $ => B      :MLOAD(escalarMulBN254_Q_x1)
    $           :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(pairingBN254_psi_x2)
    $ => B      :MLOAD(escalarMulBN254_Q_x2)
    $           :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(pairingBN254_psi_y1)
    $ => B      :MLOAD(escalarMulBN254_Q_y1)
    $           :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(pairingBN254_psi_y2)
    $ => B      :MLOAD(escalarMulBN254_Q_y2)
    $           :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)


    1n      :MSTORE(pairingBN254_f11_x)
    0n      :MSTORE(pairingBN254_f11_y)
    0n      :MSTORE(pairingBN254_f12_x)
    0n      :MSTORE(pairingBN254_f12_y)
    0n      :MSTORE(pairingBN254_f13_x)
    0n      :MSTORE(pairingBN254_f13_y)
    0n      :MSTORE(pairingBN254_f21_x)
    0n      :MSTORE(pairingBN254_f21_y)
    0n      :MSTORE(pairingBN254_f22_x)
    0n      :MSTORE(pairingBN254_f22_y)
    0n      :MSTORE(pairingBN254_f23_x)
    0n      :MSTORE(pairingBN254_f23_y)

    0 => B     :JMP(pairingBN254_end)

pairingBN254_Q_is_zero:
    ; Check that Q is zero
    0n      :MLOAD(pairingBN254_Q_x1)
    0n      :MLOAD(pairingBN254_Q_x2)
    0n      :MLOAD(pairingBN254_Q_y1)
    0n      :MLOAD(pairingBN254_Q_y2)

    ; Check that P is in G1
    ; P in G1 iff (Py)² == (Px)³ + 3 (mod p)
    ; 1] Compute LHS and RHS
    $ => A,B    :MLOAD(pairingBN254_P_x), CALL(mulFpBN254); C = (Px)²
    C => A                                         ; A = (Px)²
    $ => B      :MLOAD(pairingBN254_P_x), CALL(mulFpBN254); C = (Px)³

    3n => A     :CALL(addFpBN254)                  ; C = (Px)³ + 3
    C           :MSTORE(pairingBN254_P_x3)               ; pairingBN254_P_x3 = (Px)³ + 3

    $ => A,B    :MLOAD(pairingBN254_P_y), CALL(mulFpBN254); C = (Py)²

    ; 2] Check if LHS == RHS
    C => A
    $ => B      :MLOAD(pairingBN254_P_x3) 
    $           :EQ,JMPNC(pairingBN254_P_is_not_in_G1)

    1n      :MSTORE(pairingBN254_f11_x)
    0n      :MSTORE(pairingBN254_f11_y)
    0n      :MSTORE(pairingBN254_f12_x)
    0n      :MSTORE(pairingBN254_f12_y)
    0n      :MSTORE(pairingBN254_f13_x)
    0n      :MSTORE(pairingBN254_f13_y)
    0n      :MSTORE(pairingBN254_f21_x)
    0n      :MSTORE(pairingBN254_f21_y)
    0n      :MSTORE(pairingBN254_f22_x)
    0n      :MSTORE(pairingBN254_f22_y)
    0n      :MSTORE(pairingBN254_f23_x)
    0n      :MSTORE(pairingBN254_f23_y)

    0 => B     :JMP(pairingBN254_end)

pairingBN254_P_subgroup_check:
    ; Check that P is in G1
    ; P in G1 iff (Py)² == (Px)³ + 3 (mod p)
    ; 1] Compute LHS and RHS
    $ => A,B    :MLOAD(pairingBN254_P_x), CALL(mulFpBN254); C = (Px)²
    C => A                                         ; A = (Px)²
    $ => B      :MLOAD(pairingBN254_P_x), CALL(mulFpBN254); C = (Px)³

    3n => A     :CALL(addFpBN254)                  ; C = (Px)³ + 3
    C           :MSTORE(pairingBN254_P_x3)               ; pairingBN254_P_x3 = (Px)³ + 3

    $ => A,B    :MLOAD(pairingBN254_P_y), CALL(mulFpBN254); C = (Py)²

    ; 2] Check if LHS == RHS
    C => A
    $ => B      :MLOAD(pairingBN254_P_x3) 
    $           :EQ,JMPNC(pairingBN254_P_is_not_in_G1)

pairingBN254_Q_subgroup_check:
    ; Check that Q is in G2
    ; Q in G2 iff Q in E' and psi(Q) == [6x²]Q as proven in Proposition 3 of 2022/352

    ; 1] Check if Q is in E'(Fp2)
    ; Q in E' iff (Q.y1 + Q.y2·u)² == (Q.x1 + Q.x2·u)³ + 3/(9+u)
    ; 1.1] Compute LHS and RHS
    $ => A  :MLOAD(pairingBN254_Q_x1)
    $ => B  :MLOAD(pairingBN254_Q_x2)
            :CALL(squareFp2BN254)
    ; E + C·u = (Q.x1 + Q.x2·u)²

    E => A
    C => B
    $ => C  :MLOAD(pairingBN254_Q_x1)
    $ => D  :MLOAD(pairingBN254_Q_x2)
            :CALL(mulFp2BN254)
    ; E + C·u = (Q.x1 + Q.x2·u)³

    E => A
    C => B
    %BN254_ETWISTED_B_X => C
    %BN254_ETWISTED_B_Y => D
            :CALL(addFp2BN254)
    ; E + C·u = (Q.x1 + Q.x2·u)³ + 3/(9+u)
    E           :MSTORE(pairingBN254_Q_RHS_x)
    C           :MSTORE(pairingBN254_Q_RHS_y)

    $ => A  :MLOAD(pairingBN254_Q_y1)
    $ => B  :MLOAD(pairingBN254_Q_y2)
            :CALL(squareFp2BN254)
    ; E + C·u = (Q.y1 + Q.y2·u)²

    ; 1.2] Check if LHS == RHS
    E => A
    $ => B  :MLOAD(pairingBN254_Q_RHS_x)
    $       :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

    C => A
    $ => B  :MLOAD(pairingBN254_Q_RHS_y)
    $       :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

    ; 2] Compute psi(Q)                   
    %BN254_P => A
    $ => B      :MLOAD(pairingBN254_Q_x2)             
    $ => D      :SUB                                ; D = -Qx2
    %FROBENIUS_GAMMA121 => A
    %FROBENIUS_GAMMA122 => B
    $ => C      :MLOAD(pairingBN254_Q_x1), CALL(mulFp2BN254)
    E           :MSTORE(pairingBN254_psi_x1)
    C           :MSTORE(pairingBN254_psi_x2)

    %BN254_P => A
    $ => B      :MLOAD(pairingBN254_Q_y2)             
    $ => D      :SUB                                ; D = -Qx2
    %FROBENIUS_GAMMA131 => A
    %FROBENIUS_GAMMA132 => B
    $ => C      :MLOAD(pairingBN254_Q_y1), CALL(mulFp2BN254)
    E           :MSTORE(pairingBN254_psi_y1)
    C           :MSTORE(pairingBN254_psi_y2)

    ; 3] Compute [6x²]Q (here, we check that Q is in E')
    $ => A      :MLOAD(pairingBN254_Q_x1)
    $ => B      :MLOAD(pairingBN254_Q_x2)
    $ => C      :MLOAD(pairingBN254_Q_y1)
    $ => D      :MLOAD(pairingBN254_Q_y2)
    A           :MSTORE(escalarMulBN254_P_x1)
    B           :MSTORE(escalarMulBN254_P_x2)
    C           :MSTORE(escalarMulBN254_P_y1)
    D           :MSTORE(escalarMulBN254_P_y2)
    147946756881789318990833708069417712966n :MSTORE(escalarMulBN254_k) ; 6x²
                :CALL(escalarMulBN254)


    ; 4] Check if psi(Q) == [6x²]Q
    $ => A      :MLOAD(pairingBN254_psi_x1)
    $ => B      :MLOAD(escalarMulBN254_Q_x1)
    $           :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(pairingBN254_psi_x2)
    $ => B      :MLOAD(escalarMulBN254_Q_x2)
    $           :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(pairingBN254_psi_y1)
    $ => B      :MLOAD(escalarMulBN254_Q_y1)
    $           :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

    $ => A      :MLOAD(pairingBN254_psi_y2)
    $ => B      :MLOAD(escalarMulBN254_Q_y2)
    $           :EQ,JMPNC(pairingBN254_Q_is_not_in_G2)

pairingBN254_Miller_loop:
    $ => A      :MLOAD(pairingBN254_P_x)
    $ => B      :MLOAD(pairingBN254_P_y)
    A           :MSTORE(millerLoopBN254_P_x)
    B           :MSTORE(millerLoopBN254_P_y)
    $ => A      :MLOAD(pairingBN254_Q_x1)
    $ => B      :MLOAD(pairingBN254_Q_x2)
    $ => C      :MLOAD(pairingBN254_Q_y1)
    $ => D      :MLOAD(pairingBN254_Q_y2)
    A           :MSTORE(millerLoopBN254_Q_x1)
    B           :MSTORE(millerLoopBN254_Q_x2)
    C           :MSTORE(millerLoopBN254_Q_y1)
    D           :MSTORE(millerLoopBN254_Q_y2)
                :CALL(millerLoopBN254)
    $ => A      :MLOAD(millerLoopBN254_f11_x)
    $ => B      :MLOAD(millerLoopBN254_f11_y)
    A           :MSTORE(pairingBN254_f11_x)
    B           :MSTORE(pairingBN254_f11_y)
    $ => A      :MLOAD(millerLoopBN254_f12_x)
    $ => B      :MLOAD(millerLoopBN254_f12_y)
    A           :MSTORE(pairingBN254_f12_x)
    B           :MSTORE(pairingBN254_f12_y)
    $ => A      :MLOAD(millerLoopBN254_f13_x)
    $ => B      :MLOAD(millerLoopBN254_f13_y)
    A           :MSTORE(pairingBN254_f13_x)
    B           :MSTORE(pairingBN254_f13_y)
    $ => A      :MLOAD(millerLoopBN254_f21_x)
    $ => B      :MLOAD(millerLoopBN254_f21_y)
    A           :MSTORE(pairingBN254_f21_x)
    B           :MSTORE(pairingBN254_f21_y)
    $ => A      :MLOAD(millerLoopBN254_f22_x)
    $ => B      :MLOAD(millerLoopBN254_f22_y)
    A           :MSTORE(pairingBN254_f22_x)
    B           :MSTORE(pairingBN254_f22_y)
    $ => A      :MLOAD(millerLoopBN254_f23_x)
    $ => B      :MLOAD(millerLoopBN254_f23_y)
    A           :MSTORE(pairingBN254_f23_x)
    B           :MSTORE(pairingBN254_f23_y)


pairingBN254_final_exponentiation:
    $ => A      :MLOAD(pairingBN254_f11_x)
    $ => B      :MLOAD(pairingBN254_f11_y)
    A           :MSTORE(finalExpBN254_f11_x)
    B           :MSTORE(finalExpBN254_f11_y)
    $ => A      :MLOAD(pairingBN254_f12_x)
    $ => B      :MLOAD(pairingBN254_f12_y)
    A           :MSTORE(finalExpBN254_f12_x)
    B           :MSTORE(finalExpBN254_f12_y)
    $ => A      :MLOAD(pairingBN254_f13_x)
    $ => B      :MLOAD(pairingBN254_f13_y)
    A           :MSTORE(finalExpBN254_f13_x)
    B           :MSTORE(finalExpBN254_f13_y)
    $ => A      :MLOAD(pairingBN254_f21_x)
    $ => B      :MLOAD(pairingBN254_f21_y)
    A           :MSTORE(finalExpBN254_f21_x)
    B           :MSTORE(finalExpBN254_f21_y)
    $ => A      :MLOAD(pairingBN254_f22_x)
    $ => B      :MLOAD(pairingBN254_f22_y)
    A           :MSTORE(finalExpBN254_f22_x)
    B           :MSTORE(finalExpBN254_f22_y)
    $ => A      :MLOAD(pairingBN254_f23_x)
    $ => B      :MLOAD(pairingBN254_f23_y)
    A           :MSTORE(finalExpBN254_f23_x)
    B           :MSTORE(finalExpBN254_f23_y)
                :CALL(finalExpBN254)
    $ => A      :MLOAD(finalExpBN254_f11_x)
    $ => B      :MLOAD(finalExpBN254_f11_y)
    A           :MSTORE(pairingBN254_f11_x)
    B           :MSTORE(pairingBN254_f11_y)
    $ => A      :MLOAD(finalExpBN254_f12_x)
    $ => B      :MLOAD(finalExpBN254_f12_y)
    A           :MSTORE(pairingBN254_f12_x)
    B           :MSTORE(pairingBN254_f12_y)
    $ => A      :MLOAD(finalExpBN254_f13_x)
    $ => B      :MLOAD(finalExpBN254_f13_y)
    A           :MSTORE(pairingBN254_f13_x)
    B           :MSTORE(pairingBN254_f13_y)
    $ => A      :MLOAD(finalExpBN254_f21_x)
    $ => B      :MLOAD(finalExpBN254_f21_y)
    A           :MSTORE(pairingBN254_f21_x)
    B           :MSTORE(pairingBN254_f21_y)
    $ => A      :MLOAD(finalExpBN254_f22_x)
    $ => B      :MLOAD(finalExpBN254_f22_y)
    A           :MSTORE(pairingBN254_f22_x)
    B           :MSTORE(pairingBN254_f22_y)
    $ => A      :MLOAD(finalExpBN254_f23_x)
    $ => B      :MLOAD(finalExpBN254_f23_y)
    A           :MSTORE(pairingBN254_f23_x)
    B           :MSTORE(pairingBN254_f23_y)

    0 => B     :JMP(pairingBN254_end)

; ERRORS
pairingBN254_P_is_not_in_G1:
    1 => B      :JMP(pairingBN254_error)

pairingBN254_Q_is_not_in_G2:
    2 => B      :JMP(pairingBN254_error)

pairingBN254_error:
        0 => A

pairingBN254_end:
        $ => RR     :MLOAD(pairingBN254_RR)
        :RETURN

INCLUDE "./FRBN254/reduceFrBN254.zkasm"

INCLUDE "./FPBN254/addFpBN254.zkasm"
INCLUDE "./FPBN254/subFpBN254.zkasm"
INCLUDE "./FPBN254/mulFpBN254.zkasm"
INCLUDE "./FPBN254/squareFpBN254.zkasm"
INCLUDE "./FPBN254/invFpBN254.zkasm"

INCLUDE "./FP2BN254/addFp2BN254.zkasm"
INCLUDE "./FP2BN254/subFp2BN254.zkasm"
INCLUDE "./FP2BN254/mulFp2BN254.zkasm"
INCLUDE "./FP2BN254/squareFp2BN254.zkasm"
INCLUDE "./FP2BN254/escalarMulFp2BN254.zkasm"
INCLUDE "./FP2BN254/invFp2BN254.zkasm"

INCLUDE "./FP4BN254/squareFp4BN254.zkasm"

INCLUDE "./FP6BN254/addFp6BN254.zkasm"
INCLUDE "./FP6BN254/subFp6BN254.zkasm"
INCLUDE "./FP6BN254/mulFp6BN254.zkasm"
INCLUDE "./FP6BN254/escalarMulFp6BN254.zkasm"
INCLUDE "./FP6BN254/sparseMulAFp6BN254.zkasm"
INCLUDE "./FP6BN254/sparseMulBFp6BN254.zkasm"
INCLUDE "./FP6BN254/sparseMulCFp6BN254.zkasm"
INCLUDE "./FP6BN254/squareFp6BN254.zkasm"
INCLUDE "./FP6BN254/inverseFp6BN254.zkasm"

INCLUDE "./FP12BN254/mulFp12BN254.zkasm"
INCLUDE "./FP12BN254/sparseMulAFp12BN254.zkasm"
INCLUDE "./FP12BN254/sparseMulBFp12BN254.zkasm"
INCLUDE "./FP12BN254/squareFp12BN254.zkasm"
INCLUDE "./FP12BN254/inverseFp12BN254.zkasm"
; INCLUDE "./FP12BN254/expFp12BN254.zkasm"
INCLUDE "./FP12BN254/frobFp12BN254.zkasm"
INCLUDE "./FP12BN254/frob2Fp12BN254.zkasm"
INCLUDE "./FP12BN254/frob3Fp12BN254.zkasm"
INCLUDE "./FP12BN254/fastSquareFp12BN254.zkasm"
INCLUDE "./FP12BN254/fastExpFp12BN254.zkasm"
INCLUDE "./FP12BN254/xDecompBN254.zkasm"
INCLUDE "./FP12BN254/fastExpByXFp12BN254.zkasm"

INCLUDE "./FP12BN254/CYCLOFP12BN254/xShortDecompBN254.zkasm"
INCLUDE "./FP12BN254/CYCLOFP12BN254/compressFp12BN254.zkasm"
INCLUDE "./FP12BN254/CYCLOFP12BN254/decompressFp12BN254.zkasm"
INCLUDE "./FP12BN254/CYCLOFP12BN254/squareCycloFp12BN254.zkasm"
INCLUDE "./FP12BN254/CYCLOFP12BN254/expCycloByXFp12BN254.zkasm"

INCLUDE "./BN254/addPointBN254.zkasm"
INCLUDE "./BN254/escalarMulBN254.zkasm"
INCLUDE "./BN254/lineSamePointsBN254.zkasm"
INCLUDE "./BN254/lineDiffPointsBN254.zkasm"

INCLUDE "millerLoopBN254.zkasm"
INCLUDE "loopLengthBN254.zkasm"
INCLUDE "finalExpBN254.zkasm"