;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; POST: out is trimmed
;;
;; array_mul_short:
;;             in:
;;                  · C ∈ [1, 32], the len of inA
;;                  · inA ∈ [0, 2²⁵⁶ - 1]^C, the first input array
;;                  · inB ∈ [0, 2²⁵⁶ - 1], the second input
;;
;;          output:
;;                  · out = inA·inB, with len(out) <= C + 1
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; function array_mul_short(a: bigint[], b: bigint, base: bigint): bigint[] {
;     const alen = a.length;
;     const len = alen;
;     const result = new Array<bigint>(len).fill(0n);
;     let product: bigint;
;     let carry = 0n;
;     let i;
;     for (i = 0; i < alen; i++) {
;         product = a[i] * b + carry;
;         carry = product / base;
;         out[i] = product - carry * base;
;     }

;     if (carry > 0n) {
;         result.push(carry);
;     }

;     trim(result);
;     return result;
; }

VAR GLOBAL array_mul_short_inA[%ARRAY_MAX_LEN]
VAR GLOBAL array_mul_short_inB
VAR GLOBAL array_mul_short_out[%ARRAY_MAX_LEN_PLUS_ONE]
VAR GLOBAL array_mul_short_len_inA
VAR GLOBAL array_mul_short_len_out

VAR GLOBAL array_mul_short_RR

array_mul_short:
        %MAX_CNT_ARITH - CNT_ARITH  -  1 -   %ARRAY_MAX_LEN_MINUS_ONE       :JMPN(outOfCountersArith)
        %MAX_CNT_BINARY - CNT_BINARY                                  - 1   :JMPN(outOfCountersBinary)
        %MAX_CNT_STEPS - STEP       - 11 - 8*%ARRAY_MAX_LEN_MINUS_ONE - 7   :JMPN(outOfCountersStep)

        RR              :MSTORE(array_mul_short_RR)

        C               :MSTORE(array_mul_short_len_inA)

        ; Load b
        $ => B          :MLOAD(array_mul_short_inB)

array_mul_short_first_iteration:
        ; The result will be stored as D·base + C

        ; 1] a[0] * b, where a[i],b ∈ [0,base-1]: This number cannot be GT (base - 1)·base, two chunks
        $ => A          :MLOAD(array_mul_short_inA)
        0 => C
        $${var _arrayShortMul_AB = A*B}
        ${_arrayShortMul_AB >> 256} => D ; this is used as the next carry
        ${_arrayShortMul_AB} => C :ARITH

        ; out[0] = product - carry·base
        C               :MSTORE(array_mul_short_out)

        1 => RR
        $ - RR          :F_MLOAD(array_mul_short_len_inA), JMPZ(array_mul_short_prep_trim)

array_mul_short_loopZero2inA:
        ; The result will be stored as D·base + C

        ; 1] a[i] * b + carry, where a[i],b,carry ∈ [0,base-1]: This number cannot be GT (base - 1)·base, two chunks
        $ => A          :MLOAD(array_mul_short_inA + RR)
        D => C
        $${var _arrayShortMul_AB = A*B + C}
        ${_arrayShortMul_AB >> 256} => D ; this is used as the next carr
        ${_arrayShortMul_AB} => C :ARITH

        ; out[i] = product - carry·base
        C               :MSTORE(array_mul_short_out + RR)

        RR + 1 => RR
        $ - RR          :F_MLOAD(array_mul_short_len_inA), JMPZ(array_mul_short_prep_trim, array_mul_short_loopZero2inA)

; If the last carry > 0, we need to insert it to the output
array_mul_short_prep_trim:
        D => A
        0 => B
        $               :EQ, JMPNZ(array_mul_short_trim)

        D               :MSTORE(array_mul_short_out + RR)
        RR + 1          :MSTORE(array_mul_short_len_out), JMP(array_mul_short_end)

array_mul_short_trim:
        RR               :MSTORE(array_mul_short_len_out)

array_mul_short_end:
        $ => RR         :MLOAD(array_mul_short_RR), RETURN