;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; PRE: len(inA) >= len(inB) >= 2
;;
;; array_add_AGTB:
;;             in:
;;                  · C ∈ [1, 32], the len of inA
;;                  · D ∈ [1, 32], the len of inB
;;                  · inA ∈ [0, 2²⁵⁶ - 1]^C, the first input array
;;                  · inB ∈ [0, 2²⁵⁶ - 1]^D, the second input array
;;
;;          output:
;;                  · out = inA + inB, with len(out) <= C + 1
;;
;; NOTE: It's unoptimized for the case where len(inB) = 1. Use array_add_short instead.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; function array_add_AGTB(a: bigint[], b: bigint[], base: bigint): bigint[] {
;     const alen = a.length;
;     const blen = b.length;
;     let result = new Array<bigint>(alen);
;     let sum = 0n;
;     let carry = 0n;
;     for (let i = 0; i < blen; i++) {
;         sum = a[i] + b[i] + carry;
;         carry = sum >= base ? 1n : 0n;
;         out[i] = sum - carry * base;
;     }
;     for (let i = blen; i < alen; i++) {
;         sum = a[i] + carry;
;         carry = sum == base ? 1n : 0n; // the past carry is at most 1n
;         out[i] = sum - carry * base;
;     }

;     if (carry === 1n) {
;         result.push(carry);
;     }
;     return result;
; }

VAR GLOBAL array_add_AGTB_inA[%ARRAY_MAX_LEN]
VAR GLOBAL array_add_AGTB_inB[%ARRAY_MAX_LEN]
VAR GLOBAL array_add_AGTB_out[%ARRAY_MAX_LEN_PLUS_ONE]
VAR GLOBAL array_add_AGTB_len_inA
VAR GLOBAL array_add_AGTB_len_inB
VAR GLOBAL array_add_AGTB_len_out

VAR GLOBAL array_add_AGTB_carry

VAR GLOBAL array_add_AGTB_RR

/*
 * RESOURCES:
 * -----------------------------
 * (worst case) [steps: 7] + lenB*[steps: 14, bin: 2] + [steps: 3] + (lenA - lenB)*[steps: 9, bin: 1] + [steps: 7]
 * total: [steps: 17 + 9*lenA + 5*lenB, bin: lenA + lenB]
 * -----------------------------
 */

array_add_AGTB:
        %MAX_CNT_BINARY - CNT_BINARY     -  2*D     -   C+  D           :JMPN(outOfCountersBinary)
        %MAX_CNT_STEPS - STEP        - 5 - 14*D - 3 - 9*C+9*D - 8       :JMPN(outOfCountersStep)

        RR              :MSTORE(array_add_AGTB_RR)

        C               :MSTORE(array_add_AGTB_len_inA)
        D               :MSTORE(array_add_AGTB_len_inB)

        0 => E ; index in loops
        0               :MSTORE(array_add_AGTB_carry)
        ; [steps: 7]

array_add_AGTB_loopZero2inB:
        ; block costs -> [steps: 14, bin: 2]

        ; The result will be stored as D·base + C

        0 => D  ; reset the carry chunk

        ; a[i] + b[i], where a[i],b[i] ∈ [0,base-1]: This number cannot be GT base + (base - 2), two chunks
        $ => A          :MLOAD(array_add_AGTB_inA + E)
        $ => B          :MLOAD(array_add_AGTB_inB + E)
        $ => C          :ADD, JMPNC(__array_add_AGTB_continue_1)
        1 => D
                        __array_add_AGTB_continue_1:

        ; sum = (a[i] + b[i]) + carry: This number cannot be GT base + (base - 1), two chunks
        $ => A          :MLOAD(array_add_AGTB_carry)
        C => B
        $ => C          :ADD, JMPNC(__array_add_AGTB_continue_2)
        1 => D
                        __array_add_AGTB_continue_2:

        C               :MSTORE(array_add_AGTB_out + E)
        D               :MSTORE(array_add_AGTB_carry)

        E + 1 => E,A
        $ => B          :MLOAD(array_add_AGTB_len_inB)
        B - A           :JMPZ(array_add_AGTB_loop_index_check, array_add_AGTB_loopZero2inB)

array_add_AGTB_loop_index_check:
        $ => B          :MLOAD(array_add_AGTB_len_inA)
        B - A           :JMPZ(array_add_AGTB_check_carry)

array_add_AGTB_loopInB2InA:
        ; block costs -> [steps: 9, bin: 1]

        0 => D ; reset the carry chunk

        ; sum = a[i] + carry: This number cannot be GT base, two chunks
        $ => A          :MLOAD(array_add_AGTB_inA + E)
        $ => B          :MLOAD(array_add_AGTB_carry)
        $ => C          :ADD, JMPNC(__array_add_AGTB_continue_3)
        1 => D
                        __array_add_AGTB_continue_3:

        C               :MSTORE(array_add_AGTB_out + E)
        D               :MSTORE(array_add_AGTB_carry)

        E + 1 => E,A
        $ => B          :MLOAD(array_add_AGTB_len_inA)
        B - A           :JMPZ(array_add_AGTB_check_carry, array_add_AGTB_loopInB2InA)

array_add_AGTB_check_carry:
        D => A
        A               :JMPZ(__array_add_AGTB_continue_4)
        ; In this case, the carry = 1 and we should append it to the result
        1               :MSTORE(array_add_AGTB_out + E)
        E + 1           :MSTORE(array_add_AGTB_len_out)
                        :JMP(array_add_AGTB_end)
                        __array_add_AGTB_continue_4:
        E               :MSTORE(array_add_AGTB_len_out)

array_add_AGTB_end:
        $ => RR         :MLOAD(array_add_AGTB_RR)
                        :RETURN