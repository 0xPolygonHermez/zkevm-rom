;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; array_mul_short:
;;             in: 
;;                  · C ∈ [0, 2²⁵⁶ - 1], the len of inA
;;
;;          output: 
;;                  · out = inA·inB[0], with len(out) = C + 1
;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 

VAR GLOBAL array_mul_short_len_inA
VAR GLOBAL array_mul_short_carry

VAR GLOBAL array_mul_short_RR

array_mul_short:
        RR              :MSTORE(array_mul_short_RR)
        C               :MSTORE(array_mul_short_len_inA)
        0n => RCX   ; index in loops
        0n              :MSTORE(array_mul_short_carry)
                        :JMP(array_mul_short_loopZero2inA)

array_mul_short_carry:
        D + 1n => D
                        :JMP(return_array_mul_short_carry)

array_mul_short_loopZero2inA:
        RCX => E
        ; product = a_i * b + carry
        $ => A          :MLOAD(inA + E)
        $ => B          :MLOAD(inB)
        0n => C
        $${var _arrayShortMul_AB = A*B}
        ${_arrayShortMul_AB >> 256} => D
        ${_arrayShortMul_AB} => E :ARITH

        E => A
        $ => B          :MLOAD(array_mul_short_carry)
        $ => C          :ADD, JMPC(array_mul_short_carry)
                        return_array_mul_short_carry:
        D               :MSTORE(array_mul_short_carry)

        ; out[i] = product - carry·2²⁵⁶
        RCX => E
        C               :MSTORE(out + E)

        RCX + 1n => RCX
        RCX => A
        $ => B          :MLOAD(array_mul_short_len_inA)
        $               :EQ, JMPC(array_mul_short_carry_check)
                        :JMP(array_mul_short_loopZero2inA)

array_mul_short_carry_check:
        $ => A          :MLOAD(array_mul_short_carry)
        0n              :EQ, JMPC(array_mul_short_end)

        RCX => E
        A              :MSTORE(out + E)

array_mul_short_end:
        $ => RR         :MLOAD(array_mul_short_RR)
                        :RETURN