INCLUDE "constants.zkasm"
INCLUDE "vars.zkasm"

; Blocks zkROM
;       A - Load initial registers into memory: oldStateRoot (B), oldAccInputHash (C), oldNumBatch (SP) & chainID (GAS)
;       B - Set batch global variables
;       C - Loop parsing RLP transactions
;       D - Loop processing transactions
;       E - Batch computations: get newLocalExitRoot, assert transactions size, compute batchHashData & compute newAccInputHash
;       F - Finalize execution

start: ; main zkROM entry point
;;;;;;;;;;;;;;;;;;
;; A - Load input variabales
;;;;;;;;;;;;;;;;;;
        STEP => A
        0                                   :ASSERT ; Ensure it is the beginning of the execution
        B                                   :MSTORE(oldStateRoot)
        C                                   :MSTORE(oldAccInputHash)
        SP                                  :MSTORE(oldNumBatch)
        GAS                                 :MSTORE(chainID)

        ${getGlobalExitRoot()}              :MSTORE(globalExitRoot)
        ${getSequencerAddr()}               :MSTORE(sequencerAddr)
        ${getTimestamp()}                   :MSTORE(timestamp)
        ${getTxsLen()}                      :MSTORE(batchL2DataLength)

        B => SR ;set initial state root

        ; Increase batch number
        SP + 1                              :MSTORE(newNumBatch)

;;;;;;;;;;;;;;;;;;
;; B - Set batch global variables
;;     - set globalExitRoot in Bridge contract
;;     - load transaction count from system smart contract
;;     - compute keccaks needed to finish the batch
;;;;;;;;;;;;;;;;;;
        $${eventLog(onStartBatch, C)}

        $ => A                                  :MLOAD(globalExitRoot)
        0 => B
        $                                       :EQ, JMPC(skipSetGlobalExitRoot)

;; Set global exit root
setGlobalExitRoot:
        0 => HASHPOS
        $ => E                                  :MLOAD(lastHashKIdUsed)
        E+1 => E                                :MSTORE(lastHashKIdUsed)

        32 => D
        A                                       :HASHK(E)
        %GLOBAL_EXIT_ROOT_STORAGE_POS           :HASHK(E) ; Storage position of the global exit root map
        HASHPOS                                 :HASHKLEN(E)
        $ => C                                  :HASHKDIGEST(E)

        %ADDRESS_GLOBAL_EXIT_ROOT_MANAGER_L2 => A
        %SMT_KEY_SC_STORAGE => B

        ; read timestamp given the globalExitRoot
        ; skip overwrite timestamp if it is different than 0
        $ => D                                  :SLOAD, JMPNZ(skipSetGlobalExitRoot)

        $ => D                                  :MLOAD(timestamp)
        $ => SR                                 :SSTORE ; Store 'timestamp' in storage position 'keccak256(globalExitRoot, 0)'

skipSetGlobalExitRoot:
        SR                                      :MSTORE(batchSR)
        ; Load current tx count
        %LAST_TX_STORAGE_POS => C
        %ADDRESS_SYSTEM => A
        %SMT_KEY_SC_STORAGE => B
        $ => D          :SLOAD
        D               :MSTORE(txCount)

        ; Compute necessary keccak counters to finish batch
        $ => A          :MLOAD(batchL2DataLength)
        ; Divide the total data length + 1 by 136 to obtain the keccak counter increment.
        ; 136 is the value used by the prover to increment keccak counters
        A + 1                                   :MSTORE(arithA)
        136                                     :MSTORE(arithB), CALL(divARITH); in: [arithA, arithB] out: [arithRes1: arithA/arithB, arithRes2: arithA%arithB]
        $ => B                                  :MLOAD(arithRes1)
        ; Compute minimum necessary keccaks to finish the batch
        B + 1 + %MIN_CNT_KECCAK_BATCH           :MSTORE(cntKeccakPreProcess)
        %MAX_CNT_KECCAK_F - CNT_KECCAK_F => A
        $                                       :LT, JMPC(outOfCountersKeccak)

;;;;;;;;;;;;;;;;;;
;; C - Loop parsing RLP transactions
;;      - Load transaction RLP data and ensure it has correct RLP encoding
;;      - If an error is found in any transaction, the batch will not process any transaction
;;;;;;;;;;;;;;;;;;

        E+1 => E                            :MSTORE(lastHashKIdUsed)
        0                                   :MSTORE(batchHashPos)
        E                                   :MSTORE(batchHashDataId)
        $ => A                              :MLOAD(lastCtxUsed)
        A                                   :MSTORE(ctxTxToUse) ; Points at first context to be used when processing transactions

        $${var p = 0}

txLoopRLP:
        $ => A          :MLOAD(lastCtxUsed)
        A+1 => CTX      :MSTORE(lastCtxUsed)

        $ => A          :MLOAD(batchL2DataLength)
        $ => C          :MLOAD(batchL2DataParsed)
        C - A           :JMPN(loadTx_rlp, endCheckRLP)
endCheckRLP:
                        :JMP(txLoop)

;;;;;;;;;;;;;;;;;;
;; D - Loop processing transactions
;;      - Load transaction data and interpret it
;;;;;;;;;;;;;;;;;;

txLoop:
        $ => A          :MLOAD(pendingTxs)
        A-1 => A        :MSTORE(pendingTxs), JMPN(processTxsEnd)

        $ => A          :MLOAD(ctxTxToUse) ; Load first context used by transaction
        A+1 => CTX      :MSTORE(ctxTxToUse),JMP(processTx)

processTxEnd:
                        :CALL(updateSystemData)
processTxFinished:
        $${eventLog(onFinishTx)}   :JMP(txLoop)

processTxsEnd:

;;;;;;;;;;;;;;;;;;
;; E - Batch asserts & computations:
;;      - get newLocalExitRoot
;;      - assert transactions size
;;      - compute batchHashData
;;      - compute newAccInputHash
;;;;;;;;;;;;;;;;;;

;; Get local exit root
        ; Read 'localExitRoot' variable from GLOBAL_EXIT_ROOT_MANAGER_L2 and store
        ; it to the 'newLocalExitRoot' input
        %ADDRESS_GLOBAL_EXIT_ROOT_MANAGER_L2  => A
        %SMT_KEY_SC_STORAGE => B
        %LOCAL_EXIT_ROOT_STORAGE_POS => C
        $ => A                                          :SLOAD
        A                                               :MSTORE(newLocalExitRoot)

;; Transactions size verification
        ; Ensure bytes added to compute the 'batchHashData' matches the number of bytes loaded from input
        $ => A                          :MLOAD(batchHashPos)
        $                               :MLOAD(batchL2DataLength), ASSERT

;; Compute 'batchHashData'
        A => HASHPOS
        $ => E                          :MLOAD(batchHashDataId)

        HASHPOS                         :HASHKLEN(E)
        $ => A                          :HASHKDIGEST(E)

        A                               :MSTORE(batchHashData)

;; Compute 'newAccInputHash'
        0 => HASHPOS

        32 => D
        $ => A                          :MLOAD(oldAccInputHash)
        A                               :HASHK(0)

        $ => A                          :MLOAD(batchHashData)
        A                               :HASHK(0)

        $ => A                          :MLOAD(globalExitRoot)
        A                               :HASHK(0)

        8 => D
        $ => A                          :MLOAD(timestamp)
        A                               :HASHK(0)

        20 => D
        $ => A                          :MLOAD(sequencerAddr)
        A                               :HASHK(0)

        HASHPOS                         :HASHKLEN(0)

        $ => C                          :HASHKDIGEST(0)
        C                               :MSTORE(newAccInputHash)
        $${eventLog(onFinishBatch)}

;;;;;;;;;;;;;;;;;;
;; F - Finalize execution
;;;;;;;;;;;;;;;;;;
        ; Set output registers
        $ => D                          :MLOAD(newAccInputHash)
        $ => E                          :MLOAD(newLocalExitRoot)
        $ => PC                         :MLOAD(newNumBatch)

        ; Set registers to its initials values
        $ => B                          :MLOAD(oldStateRoot)
        $ => C                          :MLOAD(oldAccInputHash)
        $ => SP                         :MLOAD(oldNumBatch)
        $ => GAS                        :MLOAD(chainID)
        finalizeExecution:
                                        :JMP(finalWait)

INCLUDE "end.zkasm"
INCLUDE "load-tx-rlp.zkasm"
INCLUDE "process-tx.zkasm"
INCLUDE "utils.zkasm"

